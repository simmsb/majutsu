;;; majutsu-process.el --- Process handling for majutsu  -*- lexical-binding: t; -*-

;; Copyright (C) 2025 Brandon Olivier
;; Copyright (C) 2025-2026 0WD0

;; Author: Brandon Olivier
;;         0WD0 <wd.1105848296@gmail.com>
;; Maintainer: 0WD0 <wd.1105848296@gmail.com>
;; Keywords: tools, vc
;; URL: https://github.com/0WD0/majutsu

;; SPDX-License-Identifier: GPL-3.0-or-later

;; Portions of process buffer orchestration are adapted from:
;; - Magit `lisp/magit-process.el` (commit c800f79c2061621fde847f6a53129eca0e8da728)
;;   Copyright (C) 2008-2026 The Magit Project Contributors

;;; Commentary:

;; This library runs jj commands synchronously and asynchronously,
;; integrating with with-editor and handling ANSI coloring.

;;; Code:

(require 'majutsu-base)
(require 'majutsu-mode)
(require 'majutsu-jj)
(require 'majutsu-section)
(require 'ansi-color)
(require 'seq)
(require 'subr-x)
(require 'with-editor)

(require 'magit-section)
(require 'magit-process) ; for prompt functions

(declare-function majutsu-ediff--handle-control-line "majutsu-ediff" (process line))

;;; Customization

(defgroup majutsu-process nil
  "Process execution helpers for Majutsu."
  :group 'majutsu)

(defcustom majutsu-process-apply-ansi-colors t
  "When non-nil, convert ANSI escapes in jj output to text properties."
  :type 'boolean
  :group 'majutsu-process)

;;; Process buffer (Magit-style)

(defcustom majutsu-process-popup-time -1
  "Popup the process buffer if a command takes longer than this many seconds.

If -1, never popup.  If 0, popup immediately.  If a positive integer,
popup after that many seconds if the process is still running."
  :type '(choice (const :tag "Never" -1)
          (const :tag "Immediately" 0)
          (integer :tag "After this many seconds"))
  :group 'majutsu-process)

(defcustom majutsu-process-log-max 32
  "Maximum number of sections to keep in a process log buffer.

When adding a new section would go beyond the limit set here, then the
older half of the sections are removed.  Sections that belong to
processes that are still running are never removed.

When this is nil, no sections are ever removed."
  :type '(choice (const :tag "Never remove old sections" nil) integer)
  :group 'majutsu-process)

(defcustom majutsu-show-process-buffer-hint t
  "Whether to append a hint about the process buffer to JJ error messages."
  :type 'boolean
  :group 'majutsu-process)

(defcustom majutsu-jj-environment
  (list (format "INSIDE_EMACS=%s,majutsu" emacs-version))
  "Environment entries prepended while running jj commands.

Each entry must be in KEY=VALUE format and is prepended to
`process-environment' for both local and TRAMP subprocesses."
  :type '(repeat string)
  :group 'majutsu-process)

(defcustom majutsu-process-timestamp-format nil
  "Format of timestamp for each process section in the process buffer.
When non-nil, pass this to `format-time-string' and insert the result in
the heading of each process section."
  :type '(choice (const :tag "None" nil) string)
  :group 'majutsu-process)

(defvar majutsu-process--with-editor-file-roots (make-hash-table :test #'equal)
  "Map with-editor temp files to the repository roots that opened them.")

(defun majutsu-process--with-editor-open-file (process line)
  "Return the file opened by with-editor control LINE from PROCESS."
  (save-match-data
    (when (string-match with-editor-sleeping-editor-regexp line)
      (let ((arg0 (match-string 2 line))
            (arg1 (match-string 3 line))
            (dir (match-string 4 line))
            file)
        (cond
         ((string-match "\\`\\+[0-9]+\\(?::[0-9]+\\)?\\'" arg0)
          (setq file arg1))
         (t
          (setq file arg0)))
        (when file
          (unless (file-name-absolute-p file)
            (setq file (expand-file-name file dir)))
          (when-let* ((root (and process (process-get process 'default-dir)))
                      (remote (file-remote-p root)))
            (setq file (concat remote file)))
          file)))))

(defun majutsu-process-remember-with-editor-file-root (file root)
  "Remember ROOT as the repository root for with-editor FILE."
  (when (and file root)
    (puthash file (file-name-as-directory root)
             majutsu-process--with-editor-file-roots)))

(defun majutsu-process--remember-with-editor-file-root (process line)
  "Remember repository root for with-editor control LINE from PROCESS."
  (when-let* ((file (majutsu-process--with-editor-open-file process line))
              (root (or (and process (process-get process 'default-dir))
                        default-directory)))
    (majutsu-process-remember-with-editor-file-root file root)))

(defun majutsu-process-with-editor-file-root (file)
  "Return the repository root recorded for with-editor FILE."
  (and file (gethash file majutsu-process--with-editor-file-roots)))

(defun majutsu-process-forget-with-editor-file-root (file)
  "Forget the repository root recorded for with-editor FILE."
  (when file
    (remhash file majutsu-process--with-editor-file-roots)))

;;; Process buffer

(setf (alist-get 'process magit--section-type-alist) 'magit-process-section)

(defvar-keymap majutsu-process-mode-map
  :doc "Keymap for `majutsu-process-mode'."
  :parent majutsu-mode-map
  "<remap> <majutsu-refresh>" #'undefined
  "<remap> <majutsu-delete-thing>" #'majutsu-process-kill)

(define-derived-mode majutsu-process-mode majutsu-mode "Majutsu Process"
  "Mode for looking at jj process output."
  :interactive nil
  :group 'majutsu-process)

(defun majutsu-process-buffer (&optional nodisplay)
  "Display the current repository's process buffer.

If that buffer doesn't exist yet, then create it.  Non-interactively
return the buffer and unless optional NODISPLAY is non-nil also display
it."
  (interactive)
  (let* ((root (or (majutsu--buffer-root)
                   (majutsu-toplevel default-directory)
                   (majutsu--toplevel-safe default-directory)))
         (name (format "*majutsu-process: %s*"
                       (abbreviate-file-name (directory-file-name root))))
         (buffer (or (majutsu--find-mode-buffer 'majutsu-process-mode root)
                     (get-buffer-create name))))
    (with-current-buffer buffer
      (setq majutsu--default-directory root)
      (setq default-directory root)
      (if magit-root-section
          (when majutsu-process-log-max
            (majutsu--process-truncate-log))
        (majutsu-process-mode)
        (let ((inhibit-read-only t)
              (magit-insert-section--parent nil)
              (magit-insert-section--oldroot nil))
          (make-local-variable 'text-property-default-nonsticky)
          (magit-insert-section (processbuf)
            (insert "\n")))))
    (unless nodisplay
      (majutsu-display-buffer buffer))
    buffer))

(defun majutsu-process-kill ()
  "Kill the process at point."
  (interactive)
  (when-let* ((process (magit-section-value-if 'process)))
    (unless (eq (process-status process) 'run)
      (user-error "Process isn't running"))
    (kill-process process)))

(defun majutsu--process--format-arguments (program args pwd)
  (let ((prefix (and (not (equal
                           (file-name-as-directory (expand-file-name pwd))
                           (file-name-as-directory (expand-file-name default-directory))))
                     (concat (file-relative-name pwd default-directory) " "))))
    (concat prefix
            (file-name-nondirectory program)
            (and args " ")
            (mapconcat #'shell-quote-argument args " "))))

(defun majutsu--process-insert-section
    (pwd program args &optional errcode errlog face)
  (let ((inhibit-read-only t)
        (magit-insert-section--current nil)
        (magit-insert-section--parent magit-root-section)
        (magit-insert-section--oldroot nil))
    (goto-char (1- (point-max)))
    (magit-insert-section (process)
      (insert (if errcode
                  (format "%3s " (propertize (number-to-string errcode)
                                             'font-lock-face 'magit-process-ng))
                "run "))
      (when majutsu-process-timestamp-format
        (insert (format-time-string majutsu-process-timestamp-format) " "))
      (let ((cmd (majutsu--process--format-arguments program args pwd)))
        (magit-insert-heading
          (if face
              (propertize cmd 'face face)
            cmd)))
      (when errlog
        (if (bufferp errlog)
            (insert (with-current-buffer errlog
                      (buffer-substring-no-properties (point-min) (point-max))))
          (insert-file-contents errlog)
          (goto-char (1- (point-max)))))
      (insert "\n"))))

(defun majutsu--process-truncate-log ()
  (let* ((head nil)
         (tail (oref magit-root-section children))
         (count (length tail)))
    (when (and (integerp majutsu-process-log-max)
               (> (1+ count) majutsu-process-log-max))
      (while (and (cdr tail)
                  (> count (/ majutsu-process-log-max 2)))
        (let* ((inhibit-read-only t)
               (section (car tail))
               (process (oref section process)))
          (cond
           ((not process))
           ((memq (process-status process) '(exit signal))
            (delete-region (oref section start)
                           (1+ (oref section end)))
            (cl-decf count))
           (t (push section head))))
        (pop tail))
      (oset magit-root-section children
            (nconc (reverse head) tail)))))

(defvar majutsu-process-error-message-regexps
  (list "^\\*ERROR\\*: \\(.*\\)$"
        "^\\(?:Error\\|error\\): \\(.*\\)$"
        "^\\(?:fatal\\): \\(.*\\)$")
  "Regexps used to extract a one-line error summary from jj output.")

(defun majutsu--process-error-summary (process-buf section)
  "Return a one-line error summary from SECTION in PROCESS-BUF."
  (and (buffer-live-p process-buf)
       (with-current-buffer process-buf
         (and (oref section content)
              (save-excursion
                (goto-char (oref section end))
                (catch 'found
                  (dolist (re majutsu-process-error-message-regexps)
                    (when-let* ((match (save-excursion
                                         (when (re-search-backward re (oref section start) t)
                                           (string-trim (match-string 1))))))
                      (throw 'found match)))
                  nil))))))

(defun majutsu--process-error-summary-from-string (output)
  "Return a one-line error summary extracted from OUTPUT."
  (when (and (stringp output) (not (string-empty-p output)))
    (with-temp-buffer
      (insert output)
      (goto-char (point-max))
      (catch 'found
        (dolist (re majutsu-process-error-message-regexps)
          (when-let* ((match (save-excursion
                               (when (re-search-backward re nil t)
                                 (string-trim (match-string 1))))))
            (throw 'found match)))
        nil))))

(defun majutsu--process-section-output (process)
  "Return the complete output for PROCESS from the process buffer."
  (when-let* ((buf (process-buffer process))
              (section (process-get process 'section))
              (_ (buffer-live-p buf)))
    (with-current-buffer buf
      (let* ((beg (oref section content))
             (end (oref section end)))
        (cond
         ((and beg end)
          (string-trim-right
           (buffer-substring-no-properties beg end)))
         (t ""))))))

(defun majutsu--process-finish-section (section exit-code)
  (let ((inhibit-read-only t)
        (buffer (current-buffer))
        (marker (oref section start)))
    (goto-char marker)
    (save-excursion
      (delete-char 3)
      (set-marker-insertion-type marker nil)
      (insert (propertize (format "%3s" exit-code)
                          'magit-section section
                          'font-lock-face (if (= exit-code 0)
                                              'magit-process-ok
                                            'magit-process-ng)))
      (set-marker-insertion-type marker t))
    (when (and majutsu-process-apply-ansi-colors
               (oref section content))
      (ansi-color-apply-on-region (oref section content)
                                  (oref section end)))
    (cond
     ((= (oref section end)
         (+ (line-end-position) 2))
      (save-excursion
        (goto-char (1+ (line-end-position)))
        (delete-char -1)
        (oset section content nil)))
     ((and (= exit-code 0)
           (not (seq-some (lambda (window)
                            (eq (window-buffer window) buffer))
                          (window-list))))
      (majutsu-section-hide section)))))

(defun majutsu--process--error-usage (process-buf)
  (and majutsu-show-process-buffer-hint
       (if-let* ((keys (where-is-internal 'majutsu-process-buffer)))
           (format "Type %s to see %S for details"
                   (key-description (car keys)) process-buf)
         (format "See %S for details" process-buf))))

(defun majutsu-process-finish (arg &optional process-buf _command-buf default-dir section)
  "Finalize a jj process log SECTION.
ARG may be a process object or an exit code.  Return the exit code."
  (let ((process (unless (integerp arg) arg))
        exit-code)
    (unless (integerp arg)
      (setq process-buf (process-buffer arg))
      (setq default-dir (process-get arg 'default-dir))
      (setq section     (process-get arg 'section))
      (setq exit-code   (process-exit-status arg)))
    (when (integerp arg)
      (setq exit-code arg))

    (when (and (buffer-live-p process-buf) section (integerp exit-code))
      (with-current-buffer process-buf
        (majutsu--process-finish-section section exit-code)))

    (cond
     ((and (integerp exit-code) (= exit-code 0))
      (when-let* ((success-msg (and process (process-get process 'success-msg))))
        (message "%s" success-msg))
      (when-let* ((cb (and process (process-get process 'finish-callback))))
        (funcall cb process exit-code)))
     ((integerp exit-code)
      (let* ((msg (majutsu--process-error-summary process-buf section))
             (usage (majutsu--process--error-usage process-buf))
             (root default-dir))
        (when-let* ((log-buf (and root (majutsu--find-mode-buffer 'majutsu-log-mode root))))
          (with-current-buffer log-buf
            (setq-local majutsu-log--this-error (or msg "Command failed"))))
        (message "jj error: %s%s"
                 (or msg "Command failed")
                 (and usage (format " [%s]" usage))))))

    (when-let* ((cb (and process (process-get process 'finish-callback))))
      (unless (and (integerp exit-code) (= exit-code 0))
        (funcall cb process exit-code)))
    exit-code))

(defun majutsu--process-display-buffer (process)
  (when (process-live-p process)
    (let ((buf (process-buffer process)))
      (cond
       ((not (buffer-live-p buf)))
       ((= majutsu-process-popup-time 0)
        (if (minibufferp)
            (switch-to-buffer-other-window buf)
          (pop-to-buffer buf)))
       ((> majutsu-process-popup-time 0)
        (run-with-timer majutsu-process-popup-time nil
                        (lambda (p)
                          (when-let* ((_(eq (process-status p) 'run))
                                      (b (process-buffer p))
                                      (_(buffer-live-p b)))
                            (if (minibufferp)
                                (switch-to-buffer-other-window b)
                              (pop-to-buffer b))))
                        process))))))

(defun majutsu--process-setup (process section root sentinel)
  "Attach Majutsu bookkeeping to PROCESS for SECTION under ROOT.
SENTINEL becomes the process sentinel.  SECTION may be a placeholder that
is not a `magit-section', in which case the section slots are left unset."
  (set-process-query-on-exit-flag process nil)
  (process-put process 'section section)
  (process-put process 'command-buf (current-buffer))
  (process-put process 'default-dir root)
  (when (magit-process-section-p section)
    (oset section process process)
    (oset section value process))
  (with-current-buffer (process-buffer process)
    (set-marker (process-mark process) (point)))
  (majutsu--process-install-filter process)
  (set-process-sentinel process sentinel)
  process)

(defun majutsu-start-process (program &optional input &rest args)
  "Start PROGRAM asynchronously, preparing for refresh, and return the process.

PROGRAM is started using `start-file-process' and then setup to use
`majutsu--process-sentinel' and `majutsu--process-filter'.  After the
process terminates, the sentinel refreshes the buffer that was current
when this function was called (if still alive), as well as the
repository's log buffer (see `majutsu-refresh')."
  (let* ((args (flatten-tree args))
         (pwd default-directory)
         (process-buf (let ((default-directory pwd))
                        (majutsu-process-buffer t)))
         (root (with-current-buffer process-buf default-directory))
         (section (with-current-buffer process-buf
                    (prog1 (majutsu--process-insert-section pwd program args nil nil)
                      (backward-char 1))))
         (process (let ((process-environment (majutsu-process-environment args))
                        (default-process-coding-system '(utf-8-unix . utf-8-unix)))
                    (apply #'start-file-process (file-name-nondirectory program)
                           process-buf program args))))
    (majutsu--process-setup process section root #'majutsu--process-sentinel)
    (when input
      (with-current-buffer input
        (process-send-region process (point-min) (point-max))
        (process-send-eof process)))
    (majutsu--process-display-buffer process)
    process))

(defun majutsu--with-editor-control-line-p (line)
  "Return non-nil when LINE is a with-editor sleeping OPEN packet."
  (string-match-p "^WITH-EDITOR: [0-9]+ OPEN " line))

(defun majutsu--with-editor-control-fragment-p (fragment)
  "Return non-nil when FRAGMENT looks like a partial OPEN packet."
  (and (not (string-empty-p fragment))
       (or (string-prefix-p "WITH-EDITOR:" fragment)
           (string-prefix-p fragment "WITH-EDITOR:"))))

(defun majutsu--ediff-control-line-p (line)
  "Return non-nil when LINE is a Majutsu Ediff control packet."
  (string-match-p "^MAJUTSU-EDIFF: [0-9]+ \\(DIFF\\|MERGE\\) " line))

(defun majutsu--ediff-control-fragment-p (fragment)
  "Return non-nil when FRAGMENT looks like a partial Ediff packet."
  (and (not (string-empty-p fragment))
       (or (string-prefix-p "MAJUTSU-EDIFF:" fragment)
           (string-prefix-p fragment "MAJUTSU-EDIFF:"))))

(defun majutsu--process-handle-control-line (proc line)
  "Handle known control LINE values for PROC.
Return non-nil when LINE is consumed as a control packet."
  (cond
   ((majutsu--with-editor-control-line-p line)
    (majutsu-process--remember-with-editor-file-root proc line)
    t)
   ((majutsu--ediff-control-line-p line)
    (if (fboundp 'majutsu-ediff--handle-control-line)
        (condition-case err
            (majutsu-ediff--handle-control-line proc line)
          (error
           (message "Majutsu Ediff control packet failed: %s"
                    (error-message-string err))
           nil))
      nil))
   (t nil)))

(defun majutsu--process-strip-with-editor-control-packets (proc input)
  "Strip control packets from INPUT and keep partial state on PROC."
  (let ((start 0)
        (visible nil))
    (while (string-match "\n" input start)
      (let* ((end (match-beginning 0))
             (line (substring input start end)))
        (unless (majutsu--process-handle-control-line proc line)
          (push (concat line "\n") visible))
        (setq start (1+ end))))
    (let ((trailing (substring input start)))
      (cond
       ((or (majutsu--with-editor-control-fragment-p trailing)
            (majutsu--ediff-control-fragment-p trailing))
        (process-put proc 'majutsu--with-editor-filter-pending trailing))
       ((string-empty-p trailing)
        (process-put proc 'majutsu--with-editor-filter-pending ""))
       (t
        (process-put proc 'majutsu--with-editor-filter-pending "")
        (push trailing visible)))
      (apply #'concat (nreverse visible)))))

(defun majutsu--process-filter (proc string)
  "Default filter used by `majutsu-start-process'."
  (with-current-buffer (process-buffer proc)
    (let ((inhibit-read-only t))
      (goto-char (process-mark proc))
      ;; Find last ^M in STRING.  If one was found, ignore everything
      ;; before it and delete the current line.
      (when-let* ((ret-pos (cl-position ?\r string :from-end t)))
        (setq string (substring string (1+ ret-pos)))
        (delete-region (line-beginning-position) (point)))
      ;; Control packets (with-editor and Majutsu Ediff) are not user-facing
      ;; process output and should be consumed before insertion.
      (setq string
            (majutsu--process-strip-with-editor-control-packets
             proc
             (concat (or (process-get proc 'majutsu--with-editor-filter-pending) "")
                     string)))
      (unless (string-empty-p string)
        (insert (propertize string 'magit-section
                            (process-get proc 'section)))
        (magit-process-yes-or-no-prompt proc string)
        (magit-process-username-prompt proc string)
        (magit-process-password-prompt proc string)
        (run-hook-with-args-until-success 'magit-process-prompt-functions
                                          proc string))
      (set-marker (process-mark proc) (point)))))

(defun majutsu--process-install-filter (process)
  "Install Majutsu's process filter on PROCESS."
  (if (fboundp 'with-editor-set-process-filter)
      (with-editor-set-process-filter process #'majutsu--process-filter)
    (set-process-filter process #'majutsu--process-filter)))

(defun majutsu--process-sentinel (process _event)
  "Default sentinel used by `majutsu-start-process'."
  (when (memq (process-status process) '(exit signal))
    (majutsu-process-finish process)
    (unless (process-get process 'inhibit-refresh)
      (let ((command-buf (process-get process 'command-buf))
            (default-dir (process-get process 'default-dir)))
        (if (buffer-live-p command-buf)
            (with-current-buffer command-buf
              (let ((default-directory (or default-dir default-directory)))
                (majutsu-refresh)))
          (when (and default-dir (fboundp 'majutsu-log-refresh))
            (when-let* ((buffer (majutsu--find-mode-buffer 'majutsu-log-mode default-dir)))
              (with-current-buffer buffer
                (ignore-errors (majutsu-log-refresh))))))))))

(defun majutsu--process-diffstat-command-p (args)
  "Return non-nil when ARGS represent a `jj diff --stat' command."
  (and (member "diff" args)
       (member "--stat" args)))

(defun majutsu-process-environment (&optional args)
  "Return process environment used to run jj command ARGS.

A local binding of `process-environment' affects the environment used by
TRAMP subprocesses, so this function composes all process overrides in one
place similarly to Magit's `magit-process-environment'."
  (let ((env (append majutsu-jj-environment process-environment)))
    (if (and majutsu-jj-diffstat-columns
             (integerp majutsu-jj-diffstat-columns)
             (> majutsu-jj-diffstat-columns 0)
             (majutsu--process-diffstat-command-p args))
        (cons (format "COLUMNS=%d" majutsu-jj-diffstat-columns)
              (seq-remove (lambda (entry)
                            (string-prefix-p "COLUMNS=" entry))
                          env))
      env)))

(defun majutsu--process-destination-buffer (destination)
  "Return the output buffer represented by process DESTINATION."
  (cond
   ((eq destination t) (current-buffer))
   ((bufferp destination) destination)
   ((stringp destination) (get-buffer-create destination))
   ((null destination) nil)))

(defun majutsu--process-file-supported-p (infile destination)
  "Return non-nil when the responsive runner supports INFILE and DESTINATION.

The runner only handles the call shapes Majutsu actually uses: no input
file, a single stdout destination, or a (STDOUT STDERR) pair whose stderr
is discarded (nil), mixed with stdout (t), or written to a file (string)."
  (and (null infile)
       (let ((stdout (if (consp destination) (car destination) destination))
             (stderr (and (consp destination) (cadr destination))))
         (and (or (null stdout) (eq stdout t)
                  (bufferp stdout) (stringp stdout))
              (or (null stderr) (eq stderr t) (stringp stderr))))))

(defun majutsu--process-file-insert-filter (marker)
  "Return a process filter that inserts output at MARKER."
  (lambda (_process string)
    (when-let* ((buffer (marker-buffer marker)))
      (with-current-buffer buffer
        (let ((inhibit-read-only t))
          (goto-char marker)
          (insert string)
          (set-marker marker (point)))))))

(defun majutsu--process-file-created-main-process
    (before stderr-buffer stderr-process stdout-buffer stdout-filter command name)
  "Return a reliably identified main process created after BEFORE.

STDERR-PROCESS must be the new process attached to Majutsu's private
STDERR-BUFFER.  The returned process must also be new, have the corresponding
Emacs-generated NAME, and retain the exact STDOUT-BUFFER, STDOUT-FILTER, and
COMMAND passed to `make-process'.  Main and stderr process suffixes are checked
independently because Emacs uniquifies those names independently.  Return nil
instead of guessing when these constraints do not identify exactly one process."
  (let ((main-name-re (format "\\`%s\\(?:<[0-9]+>\\)?\\'"
                              (regexp-quote name)))
        (stderr-name-re (format "\\`%s stderr\\(?:<[0-9]+>\\)?\\'"
                                (regexp-quote name))))
    (when (and (processp stderr-process)
               (not (memq stderr-process before))
               (eq (process-buffer stderr-process) stderr-buffer)
               (string-match-p stderr-name-re (process-name stderr-process)))
      (let ((candidates
             (seq-filter
              (lambda (candidate)
                (and (not (memq candidate before))
                     (not (eq candidate stderr-process))
                     (string-match-p main-name-re (process-name candidate))
                     (eq (process-buffer candidate) stdout-buffer)
                     (equal (process-command candidate) command)
                     (or (null stdout-filter)
                         (eq (process-filter candidate) stdout-filter))))
              (process-list))))
        (and (null (cdr candidates))
             (car candidates))))))

(defun majutsu--process-file-responsive (program _infile destination &rest args)
  "Run PROGRAM with ARGS synchronously while servicing Emacs subprocesses.

DESTINATION follows the `process-file' stdout/stderr contract supported by
`majutsu--process-file-supported-p'.  When stderr is requested as a file,
the standard error process spawned by `make-process' has its sentinel
silenced so the captured stderr is verbatim, and is drained explicitly
before the file is written so its output cannot lag behind.  If a non-atomic
wrapper signals after creating a process, reclaim that process only when its
private stderr process and requested attributes identify it uniquely."
  (let* ((stdout-dest (if (consp destination) (car destination) destination))
         (stderr-dest (and (consp destination) (cadr destination)))
         (stderr-discard (and (consp destination) (null stderr-dest)))
         (stdout-buffer (majutsu--process-destination-buffer stdout-dest))
         (stdout-marker (and stdout-buffer
                             (copy-marker (with-current-buffer stdout-buffer
                                            (point))
                                          t)))
         (stdout-filter (and stdout-marker
                             (majutsu--process-file-insert-filter stdout-marker)))
         (stderr-buffer (and (or (stringp stderr-dest) stderr-discard)
                             (generate-new-buffer " *majutsu-stderr*")))
         (process-name (file-name-nondirectory program))
         (command (cons program args))
         (processes-before (process-list))
         process
         stderr-process
         exit)
    (unwind-protect
        (progn
          ;; Create the process inside the protected region.  In particular,
          ;; a file-handler or invalid executable can make `make-process'
          ;; signal after the temporary stderr buffer has been allocated.
          (condition-case err
              (setq process
                    (make-process
                     :name process-name
                     :buffer stdout-buffer
                     :command command
                     :connection-type 'pipe
                     :coding default-process-coding-system
                     :noquery t
                     :filter stdout-filter
                     :sentinel #'ignore
                     :stderr stderr-buffer
                     :file-handler t))
            (error
             ;; A file handler or wrapper can create the real process and
             ;; signal before returning it.  Recover that process only when
             ;; its private stderr process and all requested attributes make
             ;; the association unambiguous; otherwise leave PROCESS nil so
             ;; cleanup cannot kill an unrelated process.
             (setq stderr-process
                   (and stderr-buffer (get-buffer-process stderr-buffer)))
             (setq process
                   (majutsu--process-file-created-main-process
                    processes-before stderr-buffer stderr-process
                    stdout-buffer stdout-filter command process-name))
             (signal (car err) (cdr err))))
          (setq stderr-process
                (and stderr-buffer (get-buffer-process stderr-buffer)))
          ;; Emacs' default sentinel on the standard error process appends a
          ;; "Process ... finished" line to the stderr buffer; silence it so
          ;; captured stderr is verbatim.
          (when stderr-process
            (set-process-sentinel stderr-process #'ignore))
          ;; The main process may have already exited (e.g. it does not
          ;; read stdin); guard `process-send-eof' so we never signal
          ;; "Process ... not running" from the dispatch path.
          (when (process-live-p process)
            (condition-case nil
                (process-send-eof process)
              (error nil)))
          (setq exit (majutsu--process-wait process stderr-process))
          (when (and stderr-buffer (stringp stderr-dest))
            (with-current-buffer stderr-buffer
              (write-region (point-min) (point-max) stderr-dest nil 'silent)))
          exit)
      (when (and process (process-live-p process))
        (delete-process process))
      (when (and stderr-process (process-live-p stderr-process))
        (delete-process stderr-process))
      (when stdout-marker
        (set-marker stdout-marker nil))
      (when (buffer-live-p stderr-buffer)
        (kill-buffer stderr-buffer)))))

(defun majutsu-process-file (program &optional infile destination display &rest args)
  "Run PROGRAM synchronously like `process-file' with Majutsu process defaults.

This centralizes subprocess environment and coding behavior for jj invocations.
Unlike `process-file', this implementation waits via
`accept-process-output', so Emacs can service subprocesses such as an
Emacs-based GPG pinentry while jj is running."
  (let ((process-environment (majutsu-process-environment args))
        (default-process-coding-system '(utf-8-unix . utf-8-unix)))
    (if (or display
            (eq destination 0)
            (not (majutsu--process-file-supported-p infile destination)))
        (apply #'process-file program infile destination display args)
      (apply #'majutsu--process-file-responsive
             program infile destination args))))

(defun majutsu-process-jj (destination &rest args)
  "Run jj synchronously, sending output to DESTINATION.

DESTINATION is handled like the destination argument of `process-file'.
Resolve the appropriate jj executable and prepare ARGS using
`majutsu-process-jj-arguments'.  This low-level function preserves
`default-directory' and does not log the command or refresh Majutsu buffers."
  (apply #'majutsu-process-file
         (majutsu-jj--executable)
         nil destination nil
         (majutsu-process-jj-arguments args)))

(defun majutsu-start-jj (args &optional success-msg finish-callback)
  "Run jj ARGS asynchronously for side-effects and log output.

Return the process object.

SUCCESS-MSG is displayed on exit code 0.  When FINISH-CALLBACK is
non-nil, call it as (FINISH-CALLBACK PROCESS EXIT-CODE) after the
process terminates."
  (let* ((default-directory (majutsu--toplevel-safe default-directory))
         (jj (majutsu-jj--executable))
         (args (majutsu-process-jj-arguments args))
         (process (apply #'majutsu-start-process jj nil args)))
    (when success-msg
      (process-put process 'success-msg success-msg))
    (when finish-callback
      (process-put process 'finish-callback finish-callback))
    process))

(defun majutsu--process-wait (process &optional stderr-process)
  "Wait for PROCESS to finish while continuing to service Emacs subprocesses.

This is used for commands that are synchronous from Majutsu's point of
view, but may need another Emacs subprocess to run concurrently.  In
particular, GnuPG setups using an Emacs-based pinentry need Emacs to keep
servicing the server/pinentry process while jj is waiting for GPG.

`accept-process-output' keeps servicing every Emacs subprocess while it
waits, so pinentry stays responsive even though we block on PROCESS.  When
STDERR-PROCESS is non-nil, also drain it; per the Emacs manual, output from
a separate standard error process is not delivered by waiting on the main
process alone.

Process sentinels run with quitting inhibited, but this helper can be
re-entered from a sentinel-triggered refresh.  Locally re-enable quitting
around the blocking wait so Emacs does not warn about an uninterruptible
`accept-process-output' call.

If the user interrupts the wait with `C-g', return 255 so callers can
finalize the process section as a failed command.  In that case the
surrounding `unwind-protect' is responsible for cleaning up PROCESS and
STDERR-PROCESS; any partially captured stderr is discarded."
  (condition-case nil
      (if (with-local-quit
            (while (accept-process-output process))
            (when stderr-process
              (while (accept-process-output stderr-process)))
            t)
          (process-exit-status process)
        (setq quit-flag nil)
        255)
    (quit 255)))

(defun majutsu--call-process-responsive (program process-buf section root &rest args)
  "Run PROGRAM with ARGS synchronously without blocking Emacs subprocesses.

Output is appended to PROCESS-BUF at SECTION, using the same filter as
`majutsu-start-process'.  Return the process exit status."
  (let* ((process-environment (majutsu-process-environment args))
         (default-process-coding-system '(utf-8-unix . utf-8-unix))
         (process (apply #'start-file-process (file-name-nondirectory program)
                         process-buf program args))
         exit)
    ;; `majutsu-call-jj' finalizes the section explicitly after this helper
    ;; returns, so suppress Emacs' default sentinel, which would otherwise
    ;; append "Process ... finished" status text to the Majutsu process buffer.
    (majutsu--process-setup process section root #'ignore)
    (majutsu--process-display-buffer process)
    (unwind-protect
        (setq exit (majutsu--process-wait process))
      (when (process-live-p process)
        (delete-process process)))
    exit))

(defun majutsu-call-jj (&rest args)
  "Call jj synchronously in a separate process, for side-effects.

Process output goes into a new section in the buffer returned by
`majutsu-process-buffer'.  Return the exit code.

Unlike `majutsu-start-jj', this does not implicitly refresh any Majutsu
buffers.  Call `majutsu-refresh' explicitly when desired.

This function waits using `accept-process-output' rather than
`process-file', so Emacs can continue to service subprocesses such as an
Emacs-based GPG pinentry while jj is running."
  (let* ((default-directory (majutsu--toplevel-safe default-directory))
         (jj (majutsu-jj--executable))
         (args (majutsu-process-jj-arguments args))
         (pwd default-directory)
         (process-buf (let ((default-directory pwd))
                        (majutsu-process-buffer t)))
         (process-root (with-current-buffer process-buf default-directory)))
    (pcase-let* ((section
                  (with-current-buffer process-buf
                    (prog1 (majutsu--process-insert-section pwd jj args nil nil)
                      (backward-char 1))))
                 (inhibit-read-only t)
                 (exit (apply #'majutsu--call-process-responsive
                              jj process-buf section process-root args)))
      (setq exit (majutsu-process-finish exit process-buf (current-buffer) process-root section))
      exit)))

(defun majutsu-run-jj-async (&rest args)
  "Start jj asynchronously, preparing for refresh, and return the process.
ARGS is flattened before being passed to jj."
  (let ((flat (flatten-tree args)))
    (majutsu--message-with-log "Running %s %s"
                               (majutsu-jj--executable)
                               (string-join flat " ")))
  (majutsu-start-jj args))

(defun majutsu-run-jj (&rest args)
  "Call jj synchronously in a separate process, and refresh.

Process output goes into a new section in the buffer returned by
`majutsu-process-buffer'.  Return the exit code."
  (let ((exit (apply #'majutsu-call-jj args)))
    (majutsu-refresh)
    exit))

(defun majutsu-run-jj-with-editor (&rest args)
  "Run JJ ARGS using with-editor."
  (majutsu-with-editor (apply #'majutsu-run-jj-async args)))

;;; _
(provide 'majutsu-process)
;;; majutsu-process.el ends here
