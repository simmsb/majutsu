;;; majutsu-template.el -*- lexical-binding: t; -*-

;; Copyright (C) 2025 0WD0

;; Author: 0WD0 <wd.1105848296@gmail.com>
;; Maintainer: 0WD0 <wd.1105848296@gmail.com>
;; Keywords: tools, vc
;; URL: https://github.com/0WD0/majutsu

;;; Commentary:

;; This library implements a small Elisp DSL for composing jj template
;; strings and normalizing types.

;;; Code:

(require 'cl-lib)
(require 'subr-x)

(defgroup majutsu-template nil
  "Elisp wrapper for composing jj templates."
  :group 'majutsu)

(defvar majutsu-template--allow-eval nil
  "When non-nil, cons forms encountered during sugar transformation may be eval'd.")

;;; Self binding context

(cl-defstruct (majutsu-template--self-binding
               (:constructor majutsu-template--make-self-binding))
  "Binding describing the current implicit `self' object."
  node
  type)

(defcustom majutsu-template-default-self-type 'Commit
  "Default template type assumed for implicit `self' keywords.
Set to nil to disable keyword rewriting unless explicitly bound."
  :type '(choice (const :tag "None" nil) symbol)
  :group 'majutsu-template)

(defvar majutsu-template--self-stack nil
  "Dynamic stack describing the current implicit `self' binding.")

(defun majutsu-template--default-self-binding ()
  "Return default `self' binding."
  (majutsu-template--make-self-binding
   :node (majutsu-template--raw-node "self" majutsu-template-default-self-type)
   :type majutsu-template-default-self-type))

(defun majutsu-template--current-self ()
  "Return the current implicit `self' binding."
  (or (car majutsu-template--self-stack)
      (majutsu-template--default-self-binding)))

;;;; Type and callable metadata

(cl-defstruct (majutsu-template--type
               (:constructor majutsu-template--make-type))
  "Metadata describing a template value type."
  name
  doc
  converts-to)

(defvar majutsu-template--type-registry (make-hash-table :test #'eq)
  "Registry of known template types keyed by symbol.")

(defun majutsu-template--normalize-converts (value)
  "Normalize VALUE describing type conversions into a canonical list.
Accepts nil, a list of symbols, or a list of (TYPE . STATUS) pairs."
  (cond
   ((null value) nil)
   ((and (listp value) (consp (car value)) (symbolp (caar value)))
    value)
   ((listp value)
    (mapcar (lambda (type) (cons type 'yes)) value))
   ((symbolp value)
    (list (cons value 'yes)))
   (t
    (user-error "majutsu-template: invalid :converts specification %S" value))))

(defun majutsu-template-define-type (name &rest plist)
  "Register template type NAME with optional metadata PLIST.
Recognised keys: :doc (string), :converts or :converts-to (list)."
  (cl-check-type name symbol)
  (let* ((doc (plist-get plist :doc))
         (raw-converts (or (plist-get plist :converts)
                           (plist-get plist :converts-to)))
         (converts-to (majutsu-template--normalize-converts raw-converts)))
    (puthash name
             (majutsu-template--make-type
              :name name
              :doc doc
              :converts-to converts-to)
             majutsu-template--type-registry)))

(defun majutsu-template--lookup-type (name)
  "Return registered type metadata for NAME or nil."
  (gethash name majutsu-template--type-registry))

(eval-and-compile
  (cl-defstruct (majutsu-template--arg
                 (:constructor majutsu-template--make-arg))
    name type optional rest converts doc)

  (cl-defstruct (majutsu-template--fn
                 (:constructor majutsu-template--make-fn))
    name
    symbol
    doc
    flavor
    args
    returns
    owner
    keyword))

(eval-and-compile
  (cl-defstruct (majutsu-template--fn-flavor
                 (:constructor majutsu-template--make-flavor))
    "Metadata describing a template helper flavor."
    name
    doc
    parent
    builder)

  (defvar majutsu-template--flavor-registry (make-hash-table :test #'eq)
    "Registry of function flavors keyed by keyword.")

  (defun majutsu-template-define-flavor (name &rest plist)
    "Register helper flavor NAME with optional metadata PLIST.
Recognised keys: :doc (string), :parent (keyword), :builder (function)."
    (cl-check-type name keyword)
    (let* ((doc (plist-get plist :doc))
           (parent (plist-get plist :parent))
           (builder (plist-get plist :builder)))
      (when (and parent (not (keywordp parent)))
        (user-error "majutsu-template: flavor %S expects :parent to be a keyword, got %S"
                    name parent))
      (let ((meta (majutsu-template--make-flavor
                   :name name
                   :doc doc
                   :parent parent
                   :builder builder)))
        (when (gethash name majutsu-template--flavor-registry)
          (message "majutsu-template: redefining flavor %S" name))
        (puthash name meta majutsu-template--flavor-registry)
        meta)))

  (defun majutsu-template--lookup-flavor (name)
    "Return flavor metadata registered for NAME (keyword) or signal error."
    (unless (keywordp name)
      (user-error "majutsu-template: flavor must be keyed by keyword, got %S" name))
    (or (gethash name majutsu-template--flavor-registry)
        (user-error "majutsu-template: unknown flavor %S" name)))

  (defun majutsu-template--resolve-flavor-builder (name)
    "Return builder function associated with flavor NAME, respecting parents."
    (when name
      (let ((flavor (majutsu-template--lookup-flavor name)))
        (or (majutsu-template--fn-flavor-builder flavor)
            (let ((parent (majutsu-template--fn-flavor-parent flavor)))
              (when parent
                (majutsu-template--resolve-flavor-builder parent)))))))

  ;; TODO majutsu-template--flavor-fn-body
  (defun majutsu-template--flavor-fn-body (context)
    "Generate default body for :fn flavor using CONTEXT plist."
    (let* ((template-name (plist-get context :template-name))
           (owner (plist-get context :owner))
           (args (plist-get context :args))
           (call-args (mapcar #'majutsu-template--arg-name args)))
      (if owner
          (let ((self-arg (car call-args)))
            (unless (and self-arg (symbolp self-arg))
              (user-error "majutsu-template: method on %S must have self argument" owner))
            (setq call-args (cdr call-args))
            (list `(majutsu-template-call ,template-name ,self-arg ,@call-args)))
        (list `(majutsu-template-call ,template-name ,@call-args)))))

  (defun majutsu-template--flavor-builtin-body (context)
    "Generate default body for :builtin flavor using CONTEXT plist."
    (if (plist-get context :owner)
        nil
      (let* ((template-name (plist-get context :template-name))
             (args (plist-get context :args))
             (required (cl-remove-if
                        (lambda (arg)
                          (or (majutsu-template--arg-optional arg)
                              (majutsu-template--arg-rest arg)))
                        args))
             (optional (cl-remove-if-not #'majutsu-template--arg-optional args))
             (rest-arg (cl-find-if #'majutsu-template--arg-rest args))
             (call-args-sym (gensym "majutsu-call-args")))
        (let ((base (if required
                        `(list ,@(mapcar #'majutsu-template--arg-name required))
                      nil))
              (optional-forms
               (mapcar
                (lambda (arg)
                  (let ((name (majutsu-template--arg-name arg)))
                    `(when ,name
                       (setq ,call-args-sym (append ,call-args-sym (list ,name))))))
                optional))
              (rest-form
               (when rest-arg
                 (let ((name (majutsu-template--arg-name rest-arg)))
                   `(when ,name
                      (setq ,call-args-sym (append ,call-args-sym ,name)))))))
          (list
           `(let ((,call-args-sym ,base))
              ,@optional-forms
              ,@(when rest-form (list rest-form))
              (apply #'majutsu-template-call ,template-name ,call-args-sym)))))))

  (defun majutsu-template--flavor-map-like-body (context)
    "Generate default body for map-like flavors using CONTEXT plist."
    (let* ((args (plist-get context :args)))
      (when (= (length args) 3)
        (let* ((collection (nth 0 args))
               (var (nth 1 args))
               (body (nth 2 args)))
          (when (and (not (majutsu-template--arg-optional collection))
                     (not (majutsu-template--arg-optional var))
                     (not (majutsu-template--arg-optional body))
                     (not (majutsu-template--arg-rest collection))
                     (not (majutsu-template--arg-rest var))
                     (not (majutsu-template--arg-rest body)))
            (let ((method (plist-get context :template-name))
                  (result (plist-get context :returns)))
              (list `(majutsu-template--map-like
                      ,method
                      ,(majutsu-template--arg-name collection)
                      ,(majutsu-template--arg-name var)
                      ,(majutsu-template--arg-name body)
                      ',result))))))))

  (defun majutsu-template--flavor-body (flavor context)
    "Return auto-generated body forms for FLAVOR using CONTEXT plist."
    (let ((builder (majutsu-template--resolve-flavor-builder flavor)))
      (when builder
        (funcall builder context))))

  (majutsu-template-define-flavor :builtin
                                  :doc "Helpers that proxy jj built-in functions."
                                  :builder #'majutsu-template--flavor-builtin-body)

  (majutsu-template-define-flavor :fn
                                  :doc "Default flavor for custom helpers."
                                  :builder #'majutsu-template--flavor-fn-body)

  (majutsu-template-define-flavor :custom
                                  :doc "Blank flavor for custom helpers with no default body.")

  (majutsu-template-define-flavor :map-like
                                  :doc "Helpers producing collection.method(|var| body) nodes."
                                  :parent :fn
                                  :builder #'majutsu-template--flavor-map-like-body)

  (majutsu-template-define-flavor :filter-like :parent :map-like)
  (majutsu-template-define-flavor :any-like :parent :map-like)
  (majutsu-template-define-flavor :all-like :parent :map-like)
  (majutsu-template-define-flavor :-map-like :parent :fn)
  (majutsu-template-define-flavor :--map-like :parent :-map-like))

(defvar majutsu-template--function-registry (make-hash-table :test #'equal)
  "Map template function names to `majutsu-template--fn' metadata.")

(defvar majutsu-template--function-name-map (make-hash-table :test #'eq)
  "Lookup table from symbols/keywords to template function names (strings).")

(defvar majutsu-template--method-registry (make-hash-table :test #'equal)
  "Map (TYPE . NAME) to template method metadata (keywords included).")

(defun majutsu-template--symbol->template-name (sym)
  "Return the template name string corresponding to SYM.
Accepts symbols and keywords."
  (cond
   ((keywordp sym) (substring (symbol-name sym) 1))
   ((symbolp sym) (symbol-name sym))
   (t (user-error "majutsu-template: invalid function name %S" sym))))

(defun majutsu-template--normalize-call-name (name)
  "Return template function name string derived from NAME.
NAME may be a symbol, keyword, string, or a quoted symbol form."
  (cond
   ((and (consp name) (eq (car name) 'quote))
    (majutsu-template--symbol->template-name (cadr name)))
   ((symbolp name) (majutsu-template--symbol->template-name name))
   ((stringp name) name)
   (t (user-error "majutsu-template: unsupported call name %S" name))))

(defun majutsu-template--normalize-type-symbol (type)
  "Normalize TYPE expression to a symbol, validating format.
TYPE may be a symbol, keyword, or string."
  (cond
   ((symbolp type)
    (if (keywordp type)
        (intern (substring (symbol-name type) 1))
      type))
   ((stringp type)
    (if (and (> (length type) 0) (eq (aref type 0) ?:))
        (intern (substring type 1))
      (intern type)))
   (t
    (user-error "majutsu-template: invalid type specifier %S" type))))

(defun majutsu-template--lookup-function-meta (key)
  "Return metadata struct for function identified by KEY, or nil."
  (let* ((name (cond
                ((stringp key) key)
                (t (gethash key majutsu-template--function-name-map)))))
    (or (and name (gethash name majutsu-template--function-registry))
        (when (stringp key)
          (let* ((sym (intern-soft key))
                 (keyword (intern-soft (concat ":" key)))
                 (canonical (or (and sym (gethash sym majutsu-template--function-name-map))
                                (and keyword (gethash keyword majutsu-template--function-name-map)))))
            (when canonical
              (gethash canonical majutsu-template--function-registry)))))))

(defun majutsu-template--lookup-function-name (key)
  "Return registered template name string for KEY, or nil if unknown."
  (let ((meta (majutsu-template--lookup-function-meta key)))
    (when meta
      (majutsu-template--fn-name meta))))

(defun majutsu-template--lookup-method (owner name)
  "Return metadata for method NAME on OWNER type."
  (gethash (cons owner name) majutsu-template--method-registry))

(defun majutsu-template--lookup-keyword (owner name)
  "Return metadata for keyword NAME on OWNER type (alias of method lookup)."
  (let ((meta (majutsu-template--lookup-method owner name)))
    (when (and meta (majutsu-template--fn-keyword meta))
      meta)))

(defun majutsu-template--reserved-name-p (name)
  "Return non-nil if NAME (string) conflicts with reserved helpers."
  (ignore name)
  nil)

(defun majutsu-template--register-function (meta)
  "Register custom function described by META (a `majutsu-template--fn')."
  (let* ((name (majutsu-template--fn-name meta))
         (fn-symbol (majutsu-template--fn-symbol meta))
         (owner (majutsu-template--fn-owner meta))
         (keyword-flag (majutsu-template--fn-keyword meta))
         (sym (intern name))
         (keyword (intern (concat ":" name)))
         (pre-existing (gethash name majutsu-template--function-registry)))
    (if owner
        (progn
          (unless (majutsu-template--lookup-type owner)
            (message "majutsu-template: warning – declaring %s for unknown type %S"
                     name owner))
          (when keyword-flag
            ;; keywords are 0-argument methods; ensure signature reflects it
            (let ((args (majutsu-template--fn-args meta)))
              (when (> (length args) 1)
                (message "majutsu-template: keyword %s on %S declares more than self argument"
                         name owner))))
          (puthash (cons owner name) meta majutsu-template--method-registry))
      (when (majutsu-template--reserved-name-p name)
        (user-error "majutsu-template: %s is reserved" name))
      (when pre-existing
        (message "majutsu-template: redefining template helper %s" name))
      (puthash name meta majutsu-template--function-registry)
      (puthash sym name majutsu-template--function-name-map)
      (puthash keyword name majutsu-template--function-name-map))
    fn-symbol))

(eval-and-compile
  (defun majutsu-template--arg->metadata (arg)
    "Convert ARG struct to a plist suitable for metadata export."
    (list :name (majutsu-template--arg-name arg)
          :type (majutsu-template--arg-type arg)
          :optional (majutsu-template--arg-optional arg)
          :rest (majutsu-template--arg-rest arg)
          :converts (majutsu-template--arg-converts arg)
          :doc (majutsu-template--arg-doc arg)))

  (defun majutsu-template--parse-arg-options (name opts)
    "Internal helper to parse OPTS plist for argument NAME."
    (let ((optional nil)
          (rest nil)
          (converts nil)
          (doc nil))
      (while opts
        (let ((key (pop opts))
              (val (pop opts)))
          (pcase key
            (:optional (setq optional (not (null val))))
            (:rest (setq rest (not (null val))))
            (:converts (setq converts val))
            (:doc (setq doc val))
            (_ (user-error "majutsu-template-defun %s: unknown option %S" name key)))))
      (list optional rest converts doc)))

  (defun majutsu-template--parse-args (fn-name arg-specs)
    "Parse ARG-SPECS for FN-NAME into `majutsu-template--arg' structs.
Also validates placement of optional/rest arguments."
    (let ((parsed '())
          (rest-seen nil))
      (dolist (spec arg-specs)
        (unless (and (consp spec) (symbolp (car spec)) (>= (length spec) 2))
          (user-error "majutsu-template-defun %s: invalid parameter spec %S" fn-name spec))
        (let* ((arg-name (car spec))
               (type (cadr spec))
               (opts (cddr spec))
               (_ (unless (symbolp type)
                    (user-error "majutsu-template-defun %s: argument %s has invalid type %S"
                                fn-name arg-name type)))
               (opt-data (majutsu-template--parse-arg-options arg-name opts))
               (optional (nth 0 opt-data))
               (rest (nth 1 opt-data))
               (converts (nth 2 opt-data))
               (doc (nth 3 opt-data)))
          (when (and rest (not (null (cdr (memq spec arg-specs)))))
            (user-error "majutsu-template-defun %s: :rest parameter must be last" fn-name))
          (when (and rest rest-seen)
            (user-error "majutsu-template-defun %s: only one :rest parameter allowed" fn-name))
          (when (and rest optional)
            (user-error "majutsu-template-defun %s: parameter %s cannot be both optional and :rest"
                        fn-name arg-name))
          (when (and optional rest-seen)
            (user-error "majutsu-template-defun %s: optional parameters must precede :rest" fn-name))
          (when rest (setq rest-seen t))
          (push (majutsu-template--make-arg
                 :name arg-name
                 :type type
                 :optional optional
                 :rest rest
                 :converts converts
                 :doc doc)
                parsed)))
      (nreverse parsed)))

  (defun majutsu-template--build-lambda-list (args)
    "Return lambda list corresponding to ARGS metadata."
    (let ((required '())
          (optional '())
          (rest nil))
      (dolist (arg args)
        (when (majutsu-template--arg-rest arg)
          (when rest
            (user-error "majutsu-template: multiple :rest parameters not allowed"))
          (setq rest (majutsu-template--arg-name arg)))
        (cond
         ((majutsu-template--arg-rest arg))
         ((majutsu-template--arg-optional arg)
          (push (majutsu-template--arg-name arg) optional))
         (t
          (push (majutsu-template--arg-name arg) required))))
      (setq required (nreverse required)
            optional (nreverse optional))
      (append required
              (when optional (cons '&optional optional))
              (when rest (list '&rest rest)))))

  (defun majutsu-template--build-arg-normalizers (args)
    "Return forms that normalize function parameters described by ARGS."
    (cl-loop for arg in args
             collect
             (let ((name (majutsu-template--arg-name arg)))
               (cond
                ((majutsu-template--arg-rest arg)
                 `(setq ,name (mapcar #'majutsu-template--normalize ,name)))
                ((majutsu-template--arg-optional arg)
                 `(when ,name (setq ,name (majutsu-template--normalize ,name))))
                (t
                 `(setq ,name (majutsu-template--normalize ,name)))))))

  (defun majutsu-template--parse-signature (fn-name signature)
    "Parse SIGNATURE plist for FN-NAME, returning plist with metadata."
    (unless (and (consp signature) (eq (car signature) :returns) (cadr signature))
      (user-error "majutsu-template-defun %s: signature must start with (:returns TYPE ...)" fn-name))
    (let ((returns (cadr signature))
          (rest (cddr signature))
          (doc nil)
          (converts nil)
          (owner nil)
          (template-name nil)
          (flavor :fn)
          (keyword nil))
      (unless (symbolp returns)
        (user-error "majutsu-template-defun %s: invalid return type %S" fn-name returns))
      (while rest
        (let ((key (pop rest))
              (value (pop rest)))
          (pcase key
            (:converts (setq converts value))
            (:doc (setq doc value))
            (:owner (setq owner value))
            (:template-name (setq template-name value))
            (:flavor (setq flavor value))
            (:keyword (setq keyword value))
            (_ (user-error "majutsu-template-defun %s: unknown signature key %S" fn-name key)))))
      (when (and owner (not (symbolp owner)))
        (user-error "majutsu-template-defun %s: :owner expects a symbol, got %S" fn-name owner))
      (when (and template-name (not (stringp template-name)))
        (user-error "majutsu-template-defun %s: :template-name expects string, got %S"
                    fn-name template-name))
      (setq flavor (or flavor :fn))
      (unless (keywordp flavor)
        (user-error "majutsu-template-defun %s: :flavor expects a keyword, got %S"
                    fn-name flavor))
      (setq keyword (and keyword (not (null keyword))))
      (when (and keyword (not owner))
        (user-error "majutsu-template-defun %s: :keyword requires :owner to be specified" fn-name))
      ;; Validate flavor early to surface typos during macro expansion.
      (majutsu-template--lookup-flavor flavor)
      (list :returns returns
            :converts converts
            :doc doc
            :owner owner
            :template-name template-name
            :flavor flavor
            :keyword keyword)))

  (defun majutsu-template--compose-docstring (name base-doc args)
    "Compose docstring for helper NAME using BASE-DOC and ARGS metadata."
    (let ((header (or base-doc (format "Template helper %s." name)))
          (param-lines
           (when args
             (mapconcat
              (lambda (arg)
                (let ((arg-name (majutsu-template--arg-name arg))
                      (type (majutsu-template--arg-type arg))
                      (optional (majutsu-template--arg-optional arg))
                      (rest (majutsu-template--arg-rest arg))
                      (doc (majutsu-template--arg-doc arg)))
                  (concat "  " (symbol-name arg-name)
                          " (" (symbol-name type) ")"
                          (cond
                           (rest " [rest]")
                           (optional " [optional]")
                           (t ""))
                          (if doc
                              (format ": %s" doc)
                            ""))))
              args
              "\n"))))
      (if param-lines
          (concat header "\n\nParameters:\n" param-lines)
        header)))

  (defun majutsu-template--ensure-owner-type (owner fn-name)
    "Validate declared OWNER type for callable FN-NAME when provided."
    (when owner
      (unless (majutsu-template--lookup-type owner)
        (message "majutsu-template: warning – declaring %s for unknown type %S"
                 fn-name owner)))
    owner)

  (defun majutsu-template-def--inherit-signature (signature owner &optional keyword)
    (append signature
            (list :owner owner)
            (when keyword '(:keyword t)))))

;;;###autoload
(defmacro majutsu-template-defun (name args signature &rest body)
  "Define a majutsu template helper NAME with ARGS, SIGNATURE and BODY.
Generates `majutsu-template-NAME' and registers it for template DSL usage."
  (declare (indent defun))
  (unless (and (symbolp name) (not (keywordp name)))
    (user-error "majutsu-template-defun: NAME must be an unprefixed symbol"))
  (let* ((name-str (symbol-name name))
         (signature-info (majutsu-template--parse-signature name signature))
         (owner (majutsu-template--ensure-owner-type
                 (plist-get signature-info :owner)
                 name))
         (template-name (or (plist-get signature-info :template-name) name-str))
         (flavor (plist-get signature-info :flavor))
         (keyword-flag (plist-get signature-info :keyword))
         (fn-symbol (intern (format "majutsu-template-%s" name-str)))
         (parsed-args (majutsu-template--parse-args name args))
         (lambda-list (majutsu-template--build-lambda-list parsed-args))
         (normalizers (majutsu-template--build-arg-normalizers parsed-args))
         (return-type (plist-get signature-info :returns))
         (docstring (majutsu-template--compose-docstring
                     template-name (plist-get signature-info :doc) parsed-args))
         (arg-metadata (mapcar #'majutsu-template--arg->metadata parsed-args))
         (flavor-context (list :name name
                               :template-name template-name
                               :owner owner
                               :keyword keyword-flag
                               :args parsed-args
                               :returns return-type))
         (auto-body (majutsu-template--flavor-body flavor flavor-context))
         (body-forms (or body auto-body))
         (meta `(majutsu-template--make-fn
                 :name ,template-name
                 :symbol ',fn-symbol
                 :args ',arg-metadata
                 :returns ',return-type
                 :doc ,docstring
                 :owner ',owner
                 :flavor ',flavor
                 :keyword ',keyword-flag)))
    (unless body-forms
      (user-error "majutsu-template-defun %s: flavor %S does not provide a default body"
                  name flavor))
    `(progn
       (majutsu-template--register-function ,meta)
       (defun ,fn-symbol ,lambda-list
         ,docstring
         ,@normalizers
         (let ((majutsu-template--result (progn ,@body-forms)))
           (majutsu-template--normalize majutsu-template--result))))))

(defmacro majutsu-template-defmethod (name owner args signature &rest body)
  "Define template method NAME applicable to OWNER type.
ARGS describe parameters after the implicit SELF argument.

Methods defined with this macro are *not* treated as implicit keywords
unless the SIGNATURE explicitly provides :keyword t. Use
`majutsu-template-defkeyword' for zero-argument helpers intended to
participate in keyword sugar."
  (declare (indent defun))
  (let ((extended-args (cons `(self ,owner) args)))
    `(majutsu-template-defun ,name ,extended-args
       ,(majutsu-template-def--inherit-signature signature owner)
       ,@body)))

(defmacro majutsu-template-defkeyword (name owner signature &rest body)
  "Define keyword NAME (a 0-argument method) on OWNER type."
  (declare (indent defun))
  `(majutsu-template-defun ,name ((self ,owner))
     ,(majutsu-template-def--inherit-signature signature owner t)
     ,@body))

;;; Operator macro helpers

(defmacro majutsu-template--definfix (name token)
  "Define NAME as infix operator rendering TOKEN between two operands."
  `(majutsu-template-defun ,name ((lhs Template) (rhs Template))
     (:returns Template :doc ,(format "Infix %s operator." name))
     (majutsu-template--raw-node
      (format "(%s %s %s)"
              (majutsu-template--render-node lhs)
              ,token
              (majutsu-template--render-node rhs)))))

(defmacro majutsu-template--defprefix (name token)
  "Define NAME as prefix/unary operator rendering TOKEN before operand."
  `(majutsu-template-defun ,name ((value Template))
     (:returns Template :doc ,(format "Prefix %s operator." name))
     (majutsu-template--raw-node
      (format "(%s%s)"
              ,token
              (majutsu-template--render-node value)))))

(defmacro majutsu-template--defpassthrough (name &optional doc)
  "Define NAME as helper that simply emits NAME(ARGS...)."
  `(majutsu-template-defun ,name ((values Template :rest t))
     (:returns Template :doc ,doc :flavor :builtin)))

(defconst majutsu-template--operator-aliases
  '((sub . -))
  "Alias map from DSL operator names to actual function symbols.")

(defun majutsu-template--operator-symbol (op)
  "Return canonical operator symbol for OP (keyword or symbol)."
  (let* ((sym (cond
               ((keywordp op) (intern (substring (symbol-name op) 1)))
               (t op))))
    (or (alist-get sym majutsu-template--operator-aliases) sym)))

(majutsu-template--definfix + "+")
(majutsu-template--definfix - "-")
(majutsu-template--definfix * "*")
(majutsu-template--definfix / "/")
(majutsu-template--definfix % "%")
(majutsu-template--definfix > ">")
(majutsu-template--definfix >= ">=")
(majutsu-template--definfix < "<")
(majutsu-template--definfix <= "<=")
(majutsu-template--definfix == "==")
(majutsu-template--definfix != "!=")
(majutsu-template--definfix and "&&")
(majutsu-template--definfix or "||")
(majutsu-template--definfix concat-op "++")
(majutsu-template--definfix ++ "++")
(majutsu-template--defprefix not "!")
(majutsu-template--defprefix neg "-")

(defun majutsu-template--method-stub (&rest _args)
  "Placeholder for template methods/keywords that are not executable in Elisp."
  (error "majutsu-template: method stubs are not callable at runtime"))

(defun majutsu-template--parse-type-name (name)
  "Return canonical symbol corresponding to type NAME (string or symbol)."
  (cond
   ((symbolp name) name)
   ((stringp name)
    (let ((base (if (string-match "\\`\\([^<]+\\)" name)
                    (match-string 1 name)
                  name)))
      (intern base)))
   (t
    (error "majutsu-template: invalid type name %S" name))))

(defun majutsu-template--register-method-spec (owner method-spec)
  "Register method METADATA described by METHOD-SPEC for OWNER type."
  (let* ((method-name (car method-spec))
         (plist (cdr method-spec))
         (template-name (or (plist-get plist :template-name)
                            (symbol-name method-name)))
         (keyword-flag (and (plist-get plist :keyword) t))
         (flavor (or (plist-get plist :flavor) :builtin))
         (raw-args (plist-get plist :args))
         (returns (majutsu-template--parse-type-name
                   (or (plist-get plist :returns) 'Template)))
         (doc (plist-get plist :doc))
         (args-specs (cons `(self ,owner) (or raw-args '())))
         (parsed-args (majutsu-template--parse-args method-name args-specs))
         (owner (majutsu-template--ensure-owner-type owner method-name))
         (meta (majutsu-template--make-fn
                :name template-name
                :symbol 'majutsu-template--method-stub
                :args parsed-args
                :returns returns
                :doc doc
                :owner owner
                :flavor flavor
                :keyword keyword-flag)))
    (majutsu-template--lookup-flavor flavor)
    (majutsu-template--register-function meta)))

(defun majutsu-template--register-methods (specs)
  "Register built-in method metadata from SPECS list."
  (dolist (entry specs)
    (let ((owner (car entry))
          (methods (cdr entry)))
      (dolist (method methods)
        (majutsu-template--register-method-spec owner method)))))

(defconst majutsu-template--builtin-type-specs
  '((AnnotationLine
     :doc "Annotation/annotate line context."
     :converts ((Boolean . no) (Serialize . no) (Template . no)))
    (Boolean
     :doc "Boolean value."
     :converts ((Boolean . yes) (Serialize . yes) (Template . yes)))
    (ChangeId
     :doc "Change identifier."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (Commit
     :doc "Commit object."
     :converts ((Boolean . no) (Serialize . yes) (Template . no)))
    (CommitEvolutionEntry
     :doc "Commit evolution information."
     :converts ((Boolean . no) (Serialize . yes) (Template . no)))
    (CommitId
     :doc "Commit identifier."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (CommitRef
     :doc "Commit reference (bookmark/tag)."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (ConfigValue
     :doc "Configuration value."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (CryptographicSignature
     :doc "Cryptographic signature metadata."
     :converts ((Boolean . no) (Serialize . no) (Template . no)))
    (DiffStats
     :doc "Diff statistics histogram."
     :converts ((Boolean . no) (Serialize . no) (Template . yes)))
    (Email
     :doc "Email address component."
     :converts ((Boolean . yes) (Serialize . yes) (Template . yes)))
    (Integer
     :doc "Integer value."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (Lambda
     :doc "Template lambda/expression."
     :converts ((Boolean . no) (Serialize . no) (Template . no)))
    (List
     :doc "Generic list."
     :converts ((Boolean . yes) (Serialize . maybe) (Template . maybe)))
    (List-Trailer
     :doc "List of trailers."
     :converts ((Boolean . yes) (Serialize . maybe) (Template . maybe)))
    (ListTemplate
     :doc "List of template fragments."
     :converts ((Boolean . no) (Serialize . no) (Template . yes)))
    (Operation
     :doc "Operation object."
     :converts ((Boolean . no) (Serialize . yes) (Template . no)))
    (OperationId
     :doc "Operation identifier."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (Option
     :doc "Optional value."
     :converts ((Boolean . yes) (Serialize . maybe) (Template . maybe)))
    (RefSymbol
     :doc "Reference symbol."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (RepoPath
     :doc "Repository path."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (Serialize
     :doc "Serializable expression."
     :converts ((Serialize . yes)))
    (ShortestIdPrefix
     :doc "Shortest id prefix."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (Signature
     :doc "Commit signature."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (SizeHint
     :doc "Size hint bounds."
     :converts ((Boolean . no) (Serialize . yes) (Template . no)))
    (String
     :doc "String value."
     :converts ((Boolean . yes) (Serialize . yes) (Template . yes)))
    (Stringify
     :doc "Stringified template value."
     :converts ((Boolean . no) (Serialize . maybe) (Template . yes)))
    (StringPattern
     :doc "String pattern literal."
     :converts ((Boolean . no) (Serialize . no) (Template . no)))
    (Template
     :doc "Template fragment."
     :converts ((Boolean . no) (Serialize . no) (Template . yes)))
    (Timestamp
     :doc "Timestamp value."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (TimestampRange
     :doc "Timestamp range."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes)))
    (Trailer
     :doc "Commit trailer."
     :converts ((Boolean . no) (Serialize . no) (Template . yes)))
    (TreeDiff
     :doc "Tree diff."
     :converts ((Boolean . no) (Serialize . no) (Template . no)))
    (TreeDiffEntry
     :doc "Tree diff entry."
     :converts ((Boolean . no) (Serialize . no) (Template . no)))
    (TreeEntry
     :doc "Tree entry."
     :converts ((Boolean . no) (Serialize . no) (Template . no)))
    (WorkspaceRef
     :doc "Workspace reference."
     :converts ((Boolean . no) (Serialize . yes) (Template . yes))))
  "Built-in template types and their conversion metadata.")

(dolist (type-spec majutsu-template--builtin-type-specs)
  (apply #'majutsu-template-define-type type-spec))

(defconst majutsu-template--builtin-method-specs
  '((AnnotationLine
     (commit :returns Commit :keyword t)
     (content :returns Template :keyword t)
     (line_number :returns Integer :keyword t)
     (original_line_number :returns Integer :keyword t)
     (first_line_in_hunk :returns Boolean :keyword t))
    (Commit
     (description :returns String :keyword t)
     (trailers :returns List-Trailer :keyword t)
     (change_id :returns ChangeId :keyword t)
     (commit_id :returns CommitId :keyword t)
     (parents :returns List :keyword t)
     (author :returns Signature :keyword t)
     (committer :returns Signature :keyword t)
     (signature :returns Option :keyword t)
     (mine :returns Boolean :keyword t)
     (working_copies :returns List :keyword t)
     (current_working_copy :returns Boolean :keyword t)
     (bookmarks :returns List :keyword t)
     (local_bookmarks :returns List :keyword t)
     (remote_bookmarks :returns List :keyword t)
     (tags :returns List :keyword t)
     (git_refs :returns List :keyword t)
     (git_head :returns Boolean :keyword t)
     (divergent :returns Boolean :keyword t)
     (hidden :returns Boolean :keyword t)
     (immutable :returns Boolean :keyword t)
     (contained_in :args ((revset String)) :returns Boolean)
     (conflict :returns Boolean :keyword t)
     (empty :returns Boolean :keyword t)
     (diff :args ((files String :optional t)) :returns TreeDiff)
     (files :args ((files String :optional t)) :returns List)
     (root :returns Boolean :keyword t))
    (CommitEvolutionEntry
     (commit :returns Commit :keyword t)
     (operation :returns Operation :keyword t))
    (ChangeId
     (normal_hex :returns String :keyword t)
     (short :args ((len Integer :optional t)) :returns String)
     (shortest :args ((min_len Integer :optional t)) :returns ShortestIdPrefix))
    (CommitId
     (short :args ((len Integer :optional t)) :returns String)
     (shortest :args ((min_len Integer :optional t)) :returns ShortestIdPrefix))
    (CommitRef
     (name :returns RefSymbol :keyword t)
     (remote :returns Option :keyword t)
     (present :returns Boolean :keyword t)
     (conflict :returns Boolean :keyword t)
     (normal_target :returns Option :keyword t)
     (removed_targets :returns List :keyword t)
     (added_targets :returns List :keyword t)
     (tracked :returns Boolean :keyword t)
     (tracking_present :returns Boolean :keyword t)
     (tracking_ahead_count :returns SizeHint :keyword t)
     (tracking_behind_count :returns SizeHint :keyword t))
    (ConfigValue
     (as_boolean :returns Boolean :keyword t)
     (as_integer :returns Integer :keyword t)
     (as_string :returns String :keyword t)
     (as_string_list :returns List :keyword t))
    (CryptographicSignature
     (status :returns String :keyword t)
     (key :returns String :keyword t)
     (display :returns String :keyword t))
    (DiffStats
     (total_added :returns Integer :keyword t)
     (total_removed :returns Integer :keyword t))
    (Email
     (local :returns String :keyword t)
     (domain :returns String :keyword t))
    (List
     (len :returns Integer :keyword t)
     (join :args ((separator Template)) :returns Template)
     (filter :args ((predicate Lambda)) :returns List)
     (map :args ((mapper Lambda)) :returns ListTemplate)
     (any :args ((predicate Lambda)) :returns Boolean)
     (all :args ((predicate Lambda)) :returns Boolean))
    (List-Trailer
     (contains_key :args ((key Stringify)) :returns Boolean))
    (ListTemplate
     (join :args ((separator Template)) :returns Template))
    (Operation
     (current_operation :returns Boolean :keyword t)
     (description :returns String :keyword t)
     (id :returns OperationId :keyword t)
     (tags :returns String :keyword t)
     (time :returns TimestampRange :keyword t)
     (user :returns String :keyword t)
     (snapshot :returns Boolean :keyword t)
     (root :returns Boolean :keyword t)
     (parents :returns List :keyword t))
    (OperationId
     (short :args ((len Integer :optional t)) :returns String))
    (RepoPath
     (display :returns String :keyword t)
     (parent :returns Option :keyword t))
    (ShortestIdPrefix
     (prefix :returns String :keyword t)
     (rest :returns String :keyword t)
     (upper :returns ShortestIdPrefix :keyword t)
     (lower :returns ShortestIdPrefix :keyword t))
    (Signature
     (name :returns String :keyword t)
     (email :returns Email :keyword t)
     (timestamp :returns Timestamp :keyword t))
    (SizeHint
     (lower :returns Integer :keyword t)
     (upper :returns Option :keyword t)
     (exact :returns Option :keyword t)
     (zero :returns Boolean :keyword t))
    (String
     (len :returns Integer :keyword t)
     (contains :args ((needle Stringify)) :returns Boolean)
     (match :args ((needle StringPattern)) :returns String)
     (replace :args ((pattern StringPattern)
                     (replacement Stringify)
                     (limit Integer :optional t))
              :returns String)
     (first_line :returns String :keyword t)
     (lines :returns List :keyword t)
     (upper :returns String :keyword t)
     (lower :returns String :keyword t)
     (starts_with :args ((needle Stringify)) :returns Boolean)
     (ends_with :args ((needle Stringify)) :returns Boolean)
     (remove_prefix :args ((needle Stringify)) :returns String)
     (remove_suffix :args ((needle Stringify)) :returns String)
     (trim :returns String :keyword t)
     (trim_start :returns String :keyword t)
     (trim_end :returns String :keyword t)
     (substr :args ((start Integer) (end Integer)) :returns String)
     (escape_json :returns String :keyword t))
    (Timestamp
     (ago :returns String :keyword t)
     (format :args ((format String)) :returns String)
     (utc :returns Timestamp :keyword t)
     (local :returns Timestamp :keyword t)
     (after :args ((date String)) :returns Boolean))
    (TimestampRange
     (start :returns Timestamp :keyword t)
     (end :returns Timestamp :keyword t)
     (duration :returns String :keyword t))
    (Trailer
     (key :returns String :keyword t)
     (value :returns String :keyword t))
    (TreeDiff
     (files :returns List :keyword t)
     (color_words :args ((context Integer :optional t)) :returns Template)
     (git :args ((context Integer :optional t)) :returns Template)
     (stat :args ((width Integer :optional t)) :returns DiffStats)
     (summary :returns Template :keyword t))
    (TreeDiffEntry
     (path :returns RepoPath :keyword t)
     (status :returns String :keyword t)
     (source :returns TreeEntry :keyword t)
     (target :returns TreeEntry :keyword t))
    (TreeEntry
     (path :returns RepoPath :keyword t)
     (conflict :returns Boolean :keyword t)
     (file_type :returns String :keyword t)
     (executable :returns Boolean :keyword t))
    (WorkspaceRef
     (name :returns RefSymbol :keyword t)
     (target :returns Commit :keyword t)))
  "Built-in template method metadata.")

(majutsu-template--register-methods majutsu-template--builtin-method-specs)

;;;; AST representation

(cl-defstruct (majutsu-template-node
               (:constructor majutsu-template-node-create
                             (&key kind type value args props)))
  "Internal representation for compiled template expressions."
  kind
  type
  value
  args
  props)

(defun majutsu-template--literal-node (text &optional type props)
  "Return literal template node for TEXT with optional TYPE hint."
  (majutsu-template-node-create
   :kind :literal
   :type (or type 'String)
   :value text
   :props props))

(defun majutsu-template--raw-node (text &optional type props)
  "Return raw template node for verbatim TEXT."
  (majutsu-template-node-create
   :kind :raw
   :type (or type 'Template)
   :value text
   :props props))

(defun majutsu-template--call-node (name args &optional type props)
  "Return call node NAME with ARGS and optional TYPE metadata."
  (majutsu-template-node-create
   :kind :call
   :type type
   :value name
   :args args
   :props props))

(defun majutsu-template--expand-elisp (form)
  "Expand embedded Elisp expressions inside FORM when allowed."
  (cond
   ((majutsu-template-node-p form) form)
   ((vectorp form)
    (let ((items (mapcar #'majutsu-template--expand-elisp (append form nil))))
      (apply #'vector items)))
   ((and (consp form) (eq (car form) 'quote)) form)
   ((and (consp form) (keywordp (car form)))
    (cons (car form) (mapcar #'majutsu-template--expand-elisp (cdr form))))
   ((and (consp form) majutsu-template--allow-eval)
    (majutsu-template--expand-elisp (eval form)))
   ((consp form)
    (cons (majutsu-template--expand-elisp (car form))
          (mapcar #'majutsu-template--expand-elisp (cdr form))))
   (t form)))

(defun majutsu-template--rewrite (form)
  "Rewrite literal FORM into normalized AST nodes.
Further passes (type-checking, rendering) operate on these nodes."
  (let ((expanded (if majutsu-template--allow-eval
                      (majutsu-template--expand-elisp form)
                    form)))
    (majutsu-template--sugar-transform expanded)))

(majutsu-template-defun concat ((forms Template :rest t))
  (:returns Template :doc "concat(FORMS...)." :flavor :builtin))

(majutsu-template-defun if ((condition Template)
                            (then Template)
                            (else Template :optional t))
  (:returns Template :doc "if(COND, THEN [, ELSE])." :flavor :builtin))

(majutsu-template-defun separate ((separator Template)
                                  (forms Template :rest t))
  (:returns Template :doc "separate(SEP, FORMS...)." :flavor :builtin))

(majutsu-template-defun surround ((pre Template)
                                  (post Template)
                                  (body Template))
  (:returns Template :doc "surround(PRE, POST, BODY)." :flavor :builtin))

(majutsu-template-defun label ((label Template)
                               (content Template))
  (:returns Template :doc "label helper." :flavor :builtin))

(majutsu-template-defun json ((value Template))
  (:returns Template :doc "json(FORM)." :flavor :builtin))

;; str/raw/join are defined manually to avoid recursion or for custom logic.
;; We register their metadata here.

(majutsu-template--register-function
 (majutsu-template--make-fn
  :name "str" :symbol 'majutsu-template-str
  :args (list (majutsu-template--make-arg :name 'value :type 'Template))
  :returns 'Template :doc "String literal helper." :flavor :custom))

(majutsu-template--register-function
 (majutsu-template--make-fn
  :name "raw" :symbol 'majutsu-template-raw
  :args (list (majutsu-template--make-arg :name 'value :type 'Template)
              (majutsu-template--make-arg :name 'type :type 'Template :optional t))
  :returns 'Template :doc "Raw literal helper." :flavor :custom))

(majutsu-template-defun join ((separator Template)
                              (collection Template)
                              (var Template)
                              (body Template))
  (:returns Template :doc "map-then-join helper.")
  (majutsu-template--join-impl separator collection var body))

(majutsu-template-defun map ((collection Template)
                             (var Template)
                             (body Template))
  (:returns ListTemplate :doc "map(|var| ...) operator." :flavor :map-like))

(majutsu-template-defun filter ((collection Template)
                                (var Template)
                                (body Template))
  (:returns List :doc "filter(|var| ...) operator." :flavor :filter-like))

(majutsu-template-defun any ((collection Template)
                             (var Template)
                             (body Template))
  (:returns Boolean :doc "any(|var| ...) operator." :flavor :any-like))

(majutsu-template-defun all ((collection Template)
                             (var Template)
                             (body Template))
  (:returns Boolean :doc "all(|var| ...) operator." :flavor :all-like))

(majutsu-template-defun call ((name Template)
                              (args Template :rest t))
  (:returns Template :doc "funcall helper, with builtin function support.")
  (let* ((name-node (majutsu-template--ensure-node name))
         (identifier (majutsu-template--node->identifier name-node "call name"))
         (meta (majutsu-template--lookup-function-meta identifier))
         (arg-nodes (mapcar #'majutsu-template--ensure-node args)))
    (cond
     ((and meta (eq (majutsu-template--fn-flavor meta) :builtin))
      (majutsu-template--call-node (majutsu-template--fn-name meta) arg-nodes))
     (meta
      (apply (majutsu-template--fn-symbol meta) arg-nodes))
     (t
      (majutsu-template--call-node
       (majutsu-template--normalize-call-name identifier)
       arg-nodes)))))

(defun majutsu-template--method-node-name (node)
  "Return normalized method name string from NODE."
  (let* ((raw (majutsu-template--node->identifier node "method name")))
    (if (and (> (length raw) 0) (eq (aref raw 0) ?:))
        (substring raw 1)
      raw)))

(defun majutsu-template--method-name-node-p (node)
  "Return non-nil if NODE denotes a chained method name marker."
  (and (majutsu-template-node-p node)
       (memq (majutsu-template-node-kind node) '(:raw :literal))
       (let ((value (majutsu-template-node-value node)))
         (and (stringp value)
              (> (length value) 0)
              (eq (aref value 0) ?:)))))

(defun majutsu-template--method-name-from-node (node)
  "Return normalized method name string from NODE."
  (let ((raw (majutsu-template--node->identifier node "method name")))
    (if (and (> (length raw) 0) (eq (aref raw 0) ?:))
        (substring raw 1)
      raw)))

(defun majutsu-template--method-split-segments (initial-name nodes)
  "Split method chain into segments starting with INITIAL-NAME (string).
NODES are the remaining argument nodes. Returns list of (NAME . ARGS)."
  (let ((segments '())
        (current-name initial-name)
        (current-args '()))
    (dolist (node nodes)
      (if (majutsu-template--method-name-node-p node)
          (progn
            (push (cons current-name (nreverse current-args)) segments)
            (setq current-name (majutsu-template--method-name-from-node node)
                  current-args '()))
        (push node current-args)))
    (push (cons current-name (nreverse current-args)) segments)
    (nreverse segments)))

(majutsu-template-defun method ((object Template)
                                (name Template)
                                (args Template :rest t))
  (:returns Template :doc "Method chaining helper." :flavor :custom)
  (let* ((object-node (majutsu-template--ensure-node object))
         (object-str (majutsu-template--render-node object-node))
         (name-node (majutsu-template--ensure-node name))
         (start-name (majutsu-template--method-name-from-node name-node))
         (arg-nodes (mapcar #'majutsu-template--ensure-node args))
         (segments (majutsu-template--method-split-segments start-name arg-nodes))
         (result object-str))
    (dolist (segment segments)
      (let* ((seg-name (car segment))
             (seg-args (cdr segment))
             (arg-str (mapconcat #'majutsu-template--render-node seg-args ", ")))
        (setq result (if (= (length arg-str) 0)
                         (format "%s.%s()" result seg-name)
                       (format "%s.%s(%s)" result seg-name arg-str)))))
    (majutsu-template--raw-node result)))

(majutsu-template--defpassthrough coalesce "coalesce helper.")
(majutsu-template--defpassthrough fill "fill helper.")
(majutsu-template--defpassthrough indent "indent helper.")
(majutsu-template--defpassthrough pad_start "pad_start helper.")
(majutsu-template--defpassthrough pad_end "pad_end helper.")
(majutsu-template--defpassthrough pad_centered "pad_centered helper.")
(majutsu-template--defpassthrough truncate_start "truncate_start helper.")
(majutsu-template--defpassthrough truncate_end "truncate_end helper.")
(majutsu-template--defpassthrough hash "hash helper.")
(majutsu-template--defpassthrough stringify "stringify helper.")
(majutsu-template--defpassthrough raw_escape_sequence "raw_escape_sequence helper.")
(majutsu-template--defpassthrough config "config helper.")
;; Internal node representation: (:tag ...)

(defun majutsu-template--str-escape (s)
  "Escape S into a jj double-quoted string literal content.

Supported escapes (aligned with jj docs):
  \\\" \\\\ \\t \\r \\n \\0 \\e and generic control bytes as \\xHH.
All other characters are emitted verbatim (UTF-8 allowed)."
  (unless (stringp s)
    (user-error "majutsu-template: expected string, got %S" s))
  (apply #'concat
         (cl-loop for ch across s
                  collect
                  (pcase ch
                    (?\" "\\\"")          ; double quote
                    (?\\ "\\\\")          ; backslash
                    (?\t "\\t")            ; tab
                    (?\r "\\r")            ; carriage return
                    (?\n "\\n")            ; newline
                    (0   "\\0")            ; NUL
                    (27  "\\e")            ; ESC (0x1b)
                    (_
                     (if (or (< ch 32) (= ch 127))
                         (format "\\x%02X" ch) ; other ASCII controls as \xHH
                       (string ch)))))))

(defun majutsu-template-str (s)
  "Literal string node for jj template language."
  (let ((text (if (majutsu-template-node-p s)
                  (majutsu-template--literal-string-from-node s)
                s)))
    (majutsu-template--literal-node text 'String)))

(defun majutsu-template-raw (value &optional type)
  "Raw snippet injected verbatim. Use sparingly.
VALUE may be a string or template node. TYPE optional annotation."
  (let* ((string (if (majutsu-template-node-p value)
                     (majutsu-template--literal-string-from-node value)
                   (format "%s" value)))
         (declared (when type (majutsu-template--node->type-symbol type)))
         (props (when declared (list :declared declared))))
    (majutsu-template--raw-node string declared props)))

(defun majutsu-template--emit-call (name &rest args)
  "Low-level helper: build call node NAME with normalized ARGS.
NAME may be symbol, keyword, string, or template node."
  (let* ((identifier (majutsu-template--node->identifier name "call name"))
         (resolved (majutsu-template--normalize-call-name identifier))
         (arg-nodes (mapcar #'majutsu-template--ensure-node args)))
    (majutsu-template--call-node resolved arg-nodes)))

(defun majutsu-template--literal-string-from-node (node)
  "Return literal string content from NODE if it is :str or :raw."
  (cond
   ((majutsu-template-node-p node)
    (pcase (majutsu-template-node-kind node)
      (:literal (majutsu-template-node-value node))
      (:raw (majutsu-template-node-value node))
      (_ (user-error "majutsu-template: expected literal/raw node, got %S" node))))
   ((and (consp node) (eq (car node) :str))
    (cadr node))
   ((and (consp node) (eq (car node) :raw))
    (cadr node))
   (t
    (user-error "majutsu-template: expected literal string node, got %S" node))))

(defun majutsu-template--node->identifier (node &optional context)
  "Extract identifier-like string from NODE.
CONTEXT is used in error messages."
  (if (majutsu-template-node-p node)
      (pcase (majutsu-template-node-kind node)
        (:raw (majutsu-template-node-value node))
        (:literal (majutsu-template-node-value node))
        (_ (user-error "majutsu-template: expected identifier%s, got %S"
                       (if context (format " for %s" context) "")
                       node)))
    (majutsu-template--node->identifier (majutsu-template--normalize node) context)))

(defun majutsu-template--node->type-symbol (node)
  "Return normalized type symbol described by NODE."
  (majutsu-template--normalize-type-symbol
   (majutsu-template--node->identifier node "type")))

(defun majutsu-template--resolve-call-name (expr)
  "Resolve EXPR into a call name (string or symbol)."
  (cond
   ((or (stringp expr) (symbolp expr) (keywordp expr)) expr)
   ((majutsu-template-node-p expr)
    (majutsu-template--literal-string-from-node expr))
   ((vectorp expr)
    (let ((node (majutsu-template--sugar-transform expr)))
      (majutsu-template--literal-string-from-node node)))
   ((and (consp expr) (eq (car expr) 'quote))
    (majutsu-template--resolve-call-name (cadr expr)))
   ((consp expr)
    (majutsu-template--resolve-call-name (eval expr)))
   (t expr)))

(defun majutsu-template--join-impl (sep coll var body)
  "Return node for COLL.map(|VAR| BODY).join(SEP)."
  (let* ((sep-node (majutsu-template--normalize sep))
         (coll-node (majutsu-template--normalize coll))
         (body-node (majutsu-template--normalize body))
         (var-name (if (majutsu-template-node-p var)
                       (majutsu-template--node->identifier var "join variable")
                     (format "%s" var))))
    (majutsu-template--raw-node
     (format "%s.map(|%s| %s).join(%s)"
             (majutsu-template--render-node coll-node)
             var-name
             (majutsu-template--render-node body-node)
             (majutsu-template--render-node sep-node)))))

(defun majutsu-template--map-like (method collection var body result-type)
  "Helper to produce raw node for METHOD on COLLECTION with VAR and BODY.
RESULT-TYPE, when non-nil, is used as declared type."
  (let* ((collection-node (majutsu-template--normalize collection))
         (body-node (majutsu-template--normalize body))
         (var-name (majutsu-template--node->identifier var method))
         (rendered (format "%s.%s(|%s| %s)"
                           (majutsu-template--render-node collection-node)
                           method
                           var-name
                           (majutsu-template--render-node body-node))))
    (majutsu-template--raw-node rendered result-type
                                (when result-type (list :declared result-type)))))

(defun majutsu-template--normalize (x)
  "Normalize X into an AST node."
  (majutsu-template--rewrite x))

(defun majutsu-template--ensure-node (form)
  "Return AST node corresponding to FORM."
  (majutsu-template--normalize form))

(defun majutsu-template--render-node (node)
  "Render AST NODE to jj template string."
  (pcase (majutsu-template-node-kind node)
    (:literal
     (format "\"%s\"" (majutsu-template--str-escape (majutsu-template-node-value node))))
    (:raw
     (majutsu-template-node-value node))
    (:call
     (let* ((name (majutsu-template-node-value node))
            (args (majutsu-template-node-args node))
            (compiled-args (mapconcat #'majutsu-template--render-node args ", ")))
       (format "%s(%s)" name compiled-args)))
    (_
     (user-error "majutsu-template: unknown AST node kind %S" (majutsu-template-node-kind node)))))

(defun majutsu-template--compile-list (xs)
  (mapconcat (lambda (x)
               (majutsu-template--render-node (majutsu-template--ensure-node x)))
             xs ", "))

(defun majutsu-template--compile (form)
  "Compile FORM (node or literal) to jj template string."
  (majutsu-template--render-node (majutsu-template--ensure-node form)))

(defun majutsu-template-ast (form)
  "Return AST node representing FORM without rendering."
  (majutsu-template--ensure-node form))

;;;###autoload
(defun majutsu-template-compile (form &optional self-type)
  "Public entry point: compile FORM into a jj template string.
Optional SELF-TYPE overrides `majutsu-template-default-self-type'."
  (let ((majutsu-template-default-self-type (or self-type majutsu-template-default-self-type)))
    (majutsu-template--compile form)))

(defun majutsu-template--sugar-transform (form)
  "Transform compact FORM into the template AST."
  (cond
   ((majutsu-template-node-p form) form)
   ((and (consp form) (keywordp (car form)))
    (majutsu-template--sugar-apply (car form) (cdr form)))
   ((numberp form) (majutsu-template-raw (number-to-string form)))
   ((eq form t) (majutsu-template-raw "true"))
   ((eq form nil) (majutsu-template-raw "false"))
   ((stringp form) (majutsu-template-str form))
   ((symbolp form) (majutsu-template-raw (symbol-name form)))
   ((vectorp form)
    (let* ((list-form (append form nil))
           (normalized (if (and list-form (symbolp (car list-form)))
                           list-form
                         (cons :concat list-form))))
      (majutsu-template--sugar-transform normalized)))
   ((and (consp form) (eq (car form) 'quote))
    (majutsu-template--sugar-transform (cadr form)))
   ((and (consp form) (symbolp (car form)))
    (majutsu-template--sugar-apply (car form) (cdr form)))
   ((and majutsu-template--allow-eval (consp form))
    (majutsu-template--sugar-transform (eval form)))
   ((consp form)
    (user-error "majutsu-template: use vector syntax [:op ...], lists are not accepted: %S" form))
   (t
    (user-error "majutsu-template: unsupported literal in sugar %S" form))))

(defun majutsu-template--maybe-self-dispatch (op args)
  "Return method node if OP is a self keyword applicable in current context."
  (let* ((self-binding (majutsu-template--current-self))
         (owner (majutsu-template--self-binding-type self-binding)))
    (when (and owner (or (symbolp op) (stringp op)))
      (let* ((name (if (symbolp op)
                       (majutsu-template--symbol->template-name op)
                     op))
             (meta (majutsu-template--lookup-method owner name))
             (normalized-args (mapcar #'majutsu-template--normalize args)))
        (cond
         ((and meta (not (majutsu-template--fn-keyword meta)))
          nil)
         ((and meta
               normalized-args
               (not (majutsu-template--method-name-node-p (car normalized-args))))
          (user-error "majutsu-template: keyword %s on %S does not accept arguments"
                      name owner))
         (meta
          (let ((object (majutsu-template--self-binding-node self-binding))
                (name-node (majutsu-template--normalize op)))
            (apply #'majutsu-template-method object name-node normalized-args))))))))

(defun majutsu-template--sugar-apply (op args)
  "Dispatch helper applying OP to ARGS within sugar transformation."
  (let* ((normalized (majutsu-template--operator-symbol op))
         (meta (or (majutsu-template--lookup-function-meta op)
                   (majutsu-template--lookup-function-meta normalized)))
         (self-node (majutsu-template--maybe-self-dispatch op args)))
    (cond
     (meta
      (apply (majutsu-template--fn-symbol meta)
             (mapcar #'majutsu-template--sugar-transform args)))
     (self-node self-node)
     (t
      (user-error "majutsu-template: unknown operator %S" op)))))

;;;###autoload
(defmacro tpl (form)
  "Expand a template program FORM (must be a vector literal) into the EDSL AST."
  (unless (vectorp form)
    (user-error "tpl: top-level form must be a vector, e.g., [:concat ...]"))
  (let ((majutsu-template--allow-eval t))
    (let ((node (majutsu-template--rewrite form)))
      `(quote ,node))))

;;;###autoload
(defmacro tpl-compile (form &optional self-type)
  "Expand and compile FORM to a jj template string.
Vector literals are compiled at macro-expansion time IF SELF-TYPE is provided
as a constant.

If SELF-TYPE is not provided, compilation is deferred to runtime to respect
dynamic bindings of `majutsu-template-default-self-type'."
  (cond
   ((and (vectorp form) (or (keywordp self-type) (and (consp self-type) (eq (car self-type) 'quote))))
    (let ((majutsu-template-default-self-type (majutsu-template--normalize-type-symbol (eval self-type)))
          (majutsu-template--allow-eval t))
      (let ((node (majutsu-template--rewrite form)))
        `(majutsu-template-compile ',node))))
   ((vectorp form)
    `(let ((majutsu-template--allow-eval t))
       (majutsu-template-compile ,form ,self-type)))
   (t
    `(let ((majutsu-template--allow-eval nil))
       (majutsu-template-compile (majutsu-template--normalize ,form) ,self-type)))))

;;; _
(provide 'majutsu-template)
;;; majutsu-template.el ends here
