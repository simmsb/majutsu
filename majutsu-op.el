;;; majutsu-op.el --- JJ Operation view for majutsu  -*- lexical-binding: t; -*-

;; Copyright (C) 2025-2026 0WD0

;; Author: 0WD0 <wd.1105848296@gmail.com>
;; Maintainer: 0WD0 <wd.1105848296@gmail.com>
;; Keywords: tools, vc
;; URL: https://github.com/0WD0/majutsu

;; SPDX-License-Identifier: GPL-3.0-or-later

;;; Commentary:

;; This library provides helpers and transients for jj op workflows.

;;; Code:

(require 'cl-lib)
(require 'majutsu)
(require 'majutsu-row)
(require 'majutsu-selection)
(require 'seq)
(require 'subr-x)
(require 'transient)

(declare-function majutsu-evolog "majutsu-evolog" (revset &optional args))
(declare-function majutsu-jj-buffer-string "majutsu-jj" (&rest args))
(declare-function majutsu-jj-wash "majutsu-jj" (washer keep-error &rest args))

;;; majutsu-undo

;;;###autoload
(defun majutsu-undo ()
  "Undo the last change."
  (interactive)
  (if (not (majutsu-confirm 'undo "Undo the most recent change? "))
      (message "Undo canceled")
    (let ((revset (magit-section-value-if 'jj-commit)))
      (when (zerop (majutsu-run-jj "undo"))
        (when revset
          (majutsu-goto-commit revset))))))

;;; majutsu-redo

;;;###autoload
(defun majutsu-redo ()
  "Redo the last undone change."
  (interactive)
  (if (not (majutsu-confirm 'redo "Redo the previously undone change? "))
      (message "Redo canceled")
    (let ((revset (magit-section-value-if 'jj-commit)))
      (when (zerop (majutsu-run-jj "redo"))
        (when revset
          (majutsu-goto-commit revset))))))

;;; shared templates

(defconst majutsu-op--field-separator "\x1e"
  "Field separator used by Majutsu operation templates.")

(defconst majutsu-op--metadata-template
  (majutsu-tpl
   [:concat
    [:join "\x1e"
           [:id]
           [:id :short]
           [:user]
           [:workspace_name]
           [:method [:time :start] :format "%Y-%m-%d %H:%M:%S"]
           [:method [:time :end] :format "%Y-%m-%d %H:%M:%S"]
           [:method [:time] :duration]
           [:if [:root] "root" [:if [:snapshot] "snapshot" "op"]]
           [:description :first_line]]
    "\n"]
   'Operation)
  "Template used for internal single-operation metadata queries.")

(defconst majutsu-op--commit-summary-template
  (majutsu-tpl
   [:join "\x1e"
          [:change_id]
          [:change_id :short]
          [:commit_id]
          [:commit_id :short]
          [:if [:hidden] "hidden"]
          [:if [:conflict] "conflict"]
          [:if [:empty] "empty"]
          [:coalesce
           [:if [:description]
               [:description :first_line]]
           "(no description set)"]]
   'Commit)
  "Template injected as `templates.commit_summary' for operation diffs.")

(defun majutsu-op--machine-field (field)
  "Return FIELD without text properties or surrounding whitespace."
  (string-trim (substring-no-properties (or field ""))))

(defun majutsu-op--row-machine-field (value &optional _ctx)
  "Row postprocessor returning VALUE as a machine field."
  (majutsu-op--machine-field value))

(defun majutsu-op--split-record (line expected)
  "Split LINE into EXPECTED fields, or return nil when malformed."
  (let ((fields (majutsu--split-fields
                 line majutsu-op--field-separator expected)))
    (and (= (length fields) expected) fields)))

(defun majutsu-op--parse-metadata-line (line)
  "Parse one operation metadata LINE into a plist."
  (when-let* ((fields (majutsu-op--split-record line 9)))
    (pcase-let ((`(,op-id ,op-id-short ,user ,workspace
                   ,start-time ,end-time ,duration ,kind ,description)
                 fields))
      (list :op-id       op-id
            :op-id-short op-id-short
            :user        user
            :workspace   workspace
            :start-time  start-time
            :end-time    end-time
            :duration    duration
            :kind        kind
            :desc        description))))

(defun majutsu-op--nonempty-field-p (field)
  "Return non-nil when FIELD is present and non-empty."
  (and field (not (string-empty-p (string-trim (substring-no-properties field))))))

(defun majutsu-op--parse-commit-summary (plain-summary &optional display-summary)
  "Parse a structured commit PLAIN-SUMMARY and optional DISPLAY-SUMMARY."
  (when-let* ((fields (majutsu-op--split-record plain-summary 8)))
    (pcase-let* ((`(,change-id ,change-id-short ,commit-id ,commit-id-short
                    ,hidden ,conflict ,empty ,_description)
                  fields)
                 (display-fields (or (majutsu-op--split-record
                                      (or display-summary plain-summary) 8)
                                     fields))
                 (`(,_ ,change-id-short-display ,_ ,commit-id-short-display
                    ,_ ,_ ,_ ,description-display)
                  display-fields))
      (list :change-id               change-id
            :change-id-short         change-id-short
            :change-id-short-display change-id-short-display
            :commit-id               commit-id
            :commit-id-short         commit-id-short
            :commit-id-short-display commit-id-short-display
            :hidden                  (majutsu-op--nonempty-field-p hidden)
            :conflict                (majutsu-op--nonempty-field-p conflict)
            :empty                   (majutsu-op--nonempty-field-p empty)
            :description             description-display))))

(defun majutsu-op--diff-strip-prefix (payload)
  "Return (PREFIX TARGET OFFSET) for a diff marker PAYLOAD."
  (if (string-match "\\`\\(tracked\\|untracked\\|(added)\\|(removed)\\) \\(.*\\)\\'" payload)
      (list (match-string 1 payload)
            (match-string 2 payload)
            (match-beginning 2))
    (list nil payload 0)))

(defun majutsu-op--diff-group-kind (plain-line)
  "Return the operation diff group kind for PLAIN-LINE, or nil."
  (let ((line (string-trim plain-line)))
    (cond
     ((string= line "Changed commits:") 'commits)
     ((string-match-p "\\`Changed working copy .+:\\'" line) 'working-copy)
     ((string= line "Changed local bookmarks:") 'local-bookmarks)
     ((string= line "Changed local tags:") 'local-tags)
     ((string= line "Changed remote bookmarks:") 'remote-bookmarks)
     ((string= line "Changed remote tags:") 'remote-tags)
     ((string-match-p "\\`Changed .+:\\'" line) 'changed))))

(defun majutsu-op--diff-ref-group-p (kind)
  "Return non-nil when KIND contains named ref entries."
  (memq kind '(local-bookmarks local-tags remote-bookmarks remote-tags)))

(defun majutsu-op--diff-operation-header-p (plain-line)
  "Return non-nil when PLAIN-LINE is an op-diff from/to header."
  (string-match-p "\\`\\(?:From\\|To\\) operation:" (string-trim-left plain-line)))

(defun majutsu-op--diff-elision-line-p (plain-line)
  "Return non-nil when PLAIN-LINE is an op-diff elision line."
  (string-match-p "\\`(Elided " (string-trim plain-line)))

(defun majutsu-op--diff-warning-line-p (plain-line)
  "Return non-nil when PLAIN-LINE is a jj warning."
  (string-prefix-p "Warning:" (string-trim-left plain-line)))

(defun majutsu-op--diff-ref-header-p (plain-line group-kind)
  "Return non-nil when PLAIN-LINE names a ref in GROUP-KIND."
  (and (majutsu-op--diff-ref-group-p group-kind)
       (not (string-match-p "[+-] " plain-line))
       (string-suffix-p ":" (string-trim plain-line))))

(defun majutsu-op--parse-diff-marker-line (plain-line colored-line group-kind ref-name)
  "Parse a marker PLAIN-LINE/COLORED-LINE in GROUP-KIND under REF-NAME."
  (when (and group-kind (string-match "\\([+-]\\) \\(.*\\)\\'" plain-line))
    (let* ((marker (match-string 1 plain-line))
           (payload (match-string 2 plain-line))
           (payload-start (match-beginning 2))
           (line-type (if (memq group-kind '(commits working-copy))
                          'commit-line
                        'ref-line)))
      (pcase-let* ((`(,prefix ,target ,target-offset)
                    (majutsu-op--diff-strip-prefix payload))
                   (summary-start (+ payload-start target-offset))
                   (display-summary (substring colored-line summary-start))
                   (base (list :marker marker
                               :prefix prefix
                               :group-kind group-kind
                               :ref-name ref-name)))
        (if (string= target "(absent)")
            (list :type line-type
                  :text colored-line
                  :value (append base '(:absent t)))
          (if-let* ((summary (majutsu-op--parse-commit-summary
                              target display-summary)))
              (list :type line-type
                    :text colored-line
                    :value (append base summary))
            (list :type 'raw-line :text colored-line)))))))

(defun majutsu-op--parse-diff-output (output)
  "Parse colored jj op diff OUTPUT into renderable nodes."
  (let (nodes group-title group-text group-kind group-children ref-name ref-text ref-children)
    (cl-labels
        ((finish-ref
           ()
           (when ref-name
             (push (list :type 'ref
                         :name ref-name
                         :text ref-text
                         :children (nreverse ref-children))
                   group-children)
             (setq ref-name nil
                   ref-text nil
                   ref-children nil)))
         (finish-group
           ()
           (finish-ref)
           (when group-title
             (push (list :type 'group
                         :title group-title
                         :text group-text
                         :kind group-kind
                         :children (nreverse group-children))
                   nodes)
             (setq group-title nil
                   group-text nil
                   group-kind nil
                   group-children nil)))
         (add-node
           (node)
           (cond
            (ref-name (push node ref-children))
            (group-title (push node group-children))
            (t (push node nodes)))))
      (dolist (colored-line (split-string (or output "") "\n"))
        (let* ((plain-line (substring-no-properties colored-line))
               (trimmed (string-trim plain-line)))
          (cond
           ((string-empty-p trimmed))
           ((majutsu-op--diff-operation-header-p plain-line))
           ((majutsu-op--diff-group-kind plain-line)
            (finish-group)
            (setq group-title trimmed
                  group-text colored-line
                  group-kind (majutsu-op--diff-group-kind plain-line)))
           ((majutsu-op--diff-warning-line-p plain-line)
            (add-node (list :type 'warning :text colored-line)))
           ((majutsu-op--diff-elision-line-p plain-line)
            (add-node (list :type 'elision :text colored-line)))
           ((majutsu-op--diff-ref-header-p plain-line group-kind)
            (finish-ref)
            (setq ref-name (string-remove-suffix ":" trimmed)
                  ref-text colored-line))
           ((majutsu-op--parse-diff-marker-line
             plain-line colored-line group-kind ref-name)
            (add-node (majutsu-op--parse-diff-marker-line
                       plain-line colored-line group-kind ref-name)))
           (t
            (add-node (list :type 'raw-line :text colored-line))))))
      (finish-group)
      (nreverse nodes))))

(defun majutsu-op--commit-summary-config-arg ()
  "Return a jj --config argument for operation commit summaries."
  (concat "templates.commit_summary=" majutsu-op--commit-summary-template))

(defclass majutsu-op-option (majutsu-selection-option) ())

(defclass majutsu-op-target-prefix (transient-prefix) ())

(defun majutsu-op--section-in-lineage (types &optional section)
  "Return the first ancestor SECTION whose type is in TYPES."
  (let ((section (or section (magit-current-section)))
        (types (ensure-list types)))
    (while (and section
                (not (memq (oref section type) types)))
      (setq section (oref section parent)))
    section))

(defun majutsu-op--section-value-in-lineage (types &optional section)
  "Return the first ancestor value for section TYPES."
  (when-let* ((section (majutsu-op--section-in-lineage types section)))
    (oref section value)))

(defun majutsu-op--operation-at-point ()
  "Return the enclosing operation id at point, or nil."
  (or (majutsu-op--section-value-in-lineage 'jj-op)
      (when-let* ((endpoint (majutsu-op--section-value-in-lineage
                             'jj-op-diff-endpoint)))
        (plist-get endpoint :op-id))))

(defun majutsu-op--diff-line-at-point ()
  "Return the parsed operation diff line value at point, or nil."
  (majutsu-op--section-value-in-lineage '(jj-op-commit-line jj-op-ref-line)))

(defun majutsu-op--selection-locate (operation)
  "Locate OPERATION in the current operation buffer."
  (majutsu-selection-find-section operation 'jj-op))

(defun majutsu-op--selection-targets ()
  "Return operation ids selected from point or region."
  (or (magit-region-values 'jj-op t)
      (when-let* ((operation (majutsu-op--operation-at-point)))
        (list operation))))

(cl-defmethod transient-init-value ((obj majutsu-op-target-prefix))
  (oset obj value
        (when-let* ((operation (majutsu-op--operation-at-point)))
          (list (concat "--operation=" operation)))))

(defun majutsu-op--diff-line-evolog-revset (line)
  "Return an evolog revset for parsed operation diff LINE."
  (when-let* ((change-id (and (not (plist-get line :absent))
                              (plist-get line :change-id)))
              (commit-id (plist-get line :commit-id)))
    (format "change_id(%s) | commit_id(%s)" change-id commit-id)))

(defun majutsu-op--read-operation (prompt)
  "Read an operation id with PROMPT, defaulting to point or @."
  (let* ((default (or (majutsu-op--operation-at-point) "@"))
         (value (read-string (format "%s (default %s): " prompt default)
                             nil nil default)))
    (if (string-empty-p value) default value)))

(defconst majutsu-op--read-only-global-args
  '("--at-op=@" "--ignore-working-copy")
  "Top-level jj arguments for read-only operation queries.")

(defun majutsu-op--transient-read-operation (prompt initial-input history)
  "Read an operation for a transient option.
PROMPT, INITIAL-INPUT, and HISTORY follow transient reader conventions."
  (read-string prompt initial-input history
               (or (majutsu-op--operation-at-point) "@")))

(transient-define-argument majutsu-op-arg:--operation ()
  :description "Operation"
  :class 'majutsu-op-option
  :selection-label "[OP]"
  :selection-face '(:background "goldenrod" :foreground "black")
  :locate-fn #'majutsu-op--selection-locate
  :targets-fn #'majutsu-op--selection-targets
  :selection-toggle-key "o"
  :key "-o"
  :argument "--operation="
  :prompt "Operation: "
  :reader #'majutsu-op--transient-read-operation)

(transient-define-argument majutsu-op-arg:--from ()
  :description "From operation"
  :class 'majutsu-op-option
  :selection-label "[FROM]"
  :selection-face '(:background "dark orange" :foreground "black")
  :locate-fn #'majutsu-op--selection-locate
  :targets-fn #'majutsu-op--selection-targets
  :selection-toggle-key "f"
  :shortarg "-f"
  :argument "--from="
  :prompt "From operation: "
  :reader #'majutsu-op--transient-read-operation)

(transient-define-argument majutsu-op-arg:--to ()
  :description "To operation"
  :class 'majutsu-op-option
  :selection-label "[TO]"
  :selection-face '(:background "dark cyan" :foreground "white")
  :locate-fn #'majutsu-op--selection-locate
  :targets-fn #'majutsu-op--selection-targets
  :selection-toggle-key "t"
  :shortarg "-t"
  :argument "--to="
  :prompt "To operation: "
  :reader #'majutsu-op--transient-read-operation)

;;; op transient

;;;###autoload(autoload 'majutsu-op-transient "majutsu-op" nil t)
(transient-define-prefix majutsu-op-transient ()
  "Transient for jj operation commands."
  :man-page "jj-operation"
  :transient-non-suffix t
  [["History"
    ("l" "Log..." majutsu-op-log-transient)
    ("d" "Diff..." majutsu-op-diff-transient)]
   ["State"
    ("u" "Undo" majutsu-undo)
    ("r" "Redo" majutsu-redo)
    ("R" "Restore..." majutsu-op-restore-transient)
    ("V" "Revert..." majutsu-op-revert-transient)]])

;;; op log

(defvar-local majutsu-op-log--args nil
  "Arguments used for the current operation log buffer.")

(defcustom majutsu-op-log-columns
  '((:field op-id-short :module heading
     :template [:label "id short" [:id :short]] :face t)
    (:field root-marker :module heading
     :template [:if [:root] [:label "root" "root()"] ""] :face t)
    (:field user :module heading
     :template [:if [:root] "" [:label "user" [:user]]] :face t)
    (:field workspace :module heading
     :template [:if [:root]
                ""
                [:label "workspace_name" [:workspace_name]]]
     :face t)
    (:field time-range :module heading
     :template [:if [:root]
                ""
                [:concat
                 [:label "time end ago" [:method [:time :end] :ago]]
                 [:label "time" ", lasted "]
                 [:label "time duration" [:method [:time] :duration]]]]
     :face t)
    (:field description :module body
     :template [:if [:root]
                ""
                [:label "description first_line"
                 [:method [:description] :first_line]]]
     :face t)
    (:field attributes :module body
     :template [:if [:root]
                ""
                [:label "attributes"
                 [:method [:attributes] :replace "\n" "\x1f"]]]
     :face t)
    (:field op-id :module metadata :template [:id]
     :face nil :post majutsu-op--row-machine-field)
    (:field op-id-short :module metadata :template [:id :short]
     :face nil :post majutsu-op--row-machine-field)
    (:field kind :module metadata
     :template [:if [:root]
                "root"
                [:if [:snapshot] "snapshot" "op"]]
     :face nil :post majutsu-op--row-machine-field)
    (:field current :module metadata
     :template [:if [:current_operation] "t" ""]
     :face nil :post majutsu-op--row-machine-field)
    (:field user :module metadata :template [:user] :face nil)
    (:field workspace :module metadata :template [:workspace_name] :face nil)
    (:field time :module metadata
     :template [:method [:time :end] :format "%Y-%m-%d %H:%M:%S"]
     :face nil)
    (:field time-ago :module metadata
     :template [:method [:time :end] :ago] :face nil)
    (:field duration :module metadata
     :template [:method [:time] :duration] :face nil)
    (:field description :module metadata
     :template [:method [:description] :first_line] :face nil)
    (:field attributes :module metadata
     :template [:method [:attributes] :replace "\n" "\x1f"] :face nil))
  "Flat columns for a native compact operation row.
The visible modules mirror jj's compact formatter.  Canonical metadata is
transported separately, with complete encoded attributes as the last field."
  :type 'sexp
  :group 'majutsu
  :set (lambda (symbol value)
         (set-default symbol value)
         (setq majutsu-op-log--compiled-template-cache nil)))

(defvar majutsu-op-log--compiled-template-cache nil
  "Cached compiled operation log row template metadata.")

(defun majutsu-op-log--invalidate-template-cache (&rest _)
  "Invalidate cached operation log row template metadata."
  (setq majutsu-op-log--compiled-template-cache nil))

(when (fboundp 'add-variable-watcher)
  (add-variable-watcher 'majutsu-op-log-columns
                        #'majutsu-op-log--invalidate-template-cache))

(defun majutsu-op-log--entry-id (entry)
  "Return stable section id string from operation log ENTRY."
  (let ((op-id (majutsu-row-column entry 'op-id)))
    (if (and (stringp op-id)
             (not (string-empty-p (string-trim op-id))))
        (substring-no-properties op-id)
      "unknown")))

(defun majutsu-op-log--row-profile ()
  "Return the row profile for operation log entries."
  (majutsu-row-make-profile
   :name 'op-log
   :self-type 'Operation
   :columns-var 'majutsu-op-log-columns
   :entry-id-function 'majutsu-op-log--entry-id
   :section-class 'jj-op
   :section-value-function 'majutsu-op-log--entry-id))

(defun majutsu-op-log--compile-columns ()
  "Compile operation log columns into row metadata."
  (majutsu-row-compile (majutsu-op-log--row-profile)))

(defun majutsu-op-log--ensure-template ()
  "Return cached compiled operation log template metadata."
  (or majutsu-op-log--compiled-template-cache
      (setq majutsu-op-log--compiled-template-cache
            (majutsu-op-log--compile-columns))))

(defun majutsu-op-log-arguments ()
  "Return operation log arguments from the active transient, if any."
  (if (eq transient-current-command 'majutsu-op-log-transient)
      (transient-args 'majutsu-op-log-transient)
    '()))

(defun majutsu-op-log--validate-args (args)
  "Return normalized safe operation log ARGS or signal `user-error'.
Majutsu owns the template and row framing.  Only one limit, reverse-order
switch, and graph switch are allowed."
  (unless (listp args)
    (user-error "Operation log arguments must be a list, got %S" args))
  (let ((rest args)
        (seen (make-hash-table :test #'eq))
        normalized)
    (while rest
      (let ((arg (pop rest)))
        (unless (stringp arg)
          (user-error "Unsupported operation log argument: %S" arg))
        (pcase-let*
            ((`(,kind ,value)
              (cond
               ((string-match "\\`--limit=\\([0-9]+\\)\\'" arg)
                (list 'limit (format "--limit=%s" (match-string 1 arg))))
               ((member arg '("-n" "--limit"))
                (let ((limit (pop rest)))
                  (unless (and (stringp limit)
                               (string-match-p "\\`[0-9]+\\'" limit))
                    (user-error "Invalid operation log limit: %S" limit))
                  (list 'limit (format "--limit=%s" limit))))
               ((equal arg "--reversed")
                '(reversed "--reversed"))
               ((member arg '("-G" "--no-graph"))
                '(no-graph "--no-graph"))
               (t
                (user-error "Unsupported operation log argument: %s" arg)))))
          (when (gethash kind seen)
            (user-error "Duplicate operation log argument: %s" arg))
          (puthash kind t seen)
          (push value normalized))))
    (nreverse normalized)))

(defun majutsu-op--log-command-args (&optional args)
  "Return jj arguments for operation log ARGS."
  (let ((args (majutsu-op-log--validate-args
               (or args majutsu-op-log--args))))
    (append majutsu-op--read-only-global-args
            majutsu-row-protocol-global-args
            '("op" "log")
            args
            (list "-T" (plist-get (majutsu-op-log--ensure-template)
                                  :template)))))

(defun majutsu-op--wash-log-output (_args)
  "Wash raw `jj op log` output in the current narrowed region."
  (majutsu-row-wash-buffer (majutsu-op-log--ensure-template)))

(defun majutsu-op-log-insert-entries ()
  "Insert operation log entries."
  (magit-insert-section (jj-op-log)
    (magit-insert-heading "Operation Log")
    (majutsu-row-clear-buffer-data)
    (apply #'majutsu-jj-wash
           #'majutsu-op--wash-log-output
           nil
           (majutsu-op--log-command-args))))

(defun majutsu-op-log-render ()
  "Render the op log buffer."
  (magit-insert-section (oplog)
    (majutsu-op-log-insert-entries))
  (majutsu-selection-render))

(defun majutsu-op-log-refresh-buffer ()
  "Refresh the op log buffer."
  (interactive)
  (majutsu--assert-mode 'majutsu-op-log-mode)
  (majutsu-row-clear-buffer-data)
  (majutsu-op-log-render))

(defun majutsu-op-log-restore-at-point ()
  "Restore the repository to the operation at point."
  (interactive)
  (if-let* ((op-id (majutsu-op--operation-at-point)))
      (majutsu-op-restore op-id)
    (user-error "No operation at point")))

(defun majutsu-op-log-revert-at-point ()
  "Revert the operation at point."
  (interactive)
  (if-let* ((op-id (majutsu-op--operation-at-point)))
      (majutsu-op-revert op-id)
    (user-error "No operation at point")))

;;;###autoload
(defun majutsu-op-log-copy-operation-id ()
  "Copy the current operation log entry id."
  (interactive)
  (majutsu-row-copy-entry-field-at-point 'op-id "No operation at point"))

;;;###autoload(autoload 'majutsu-op-log-copy-transient "majutsu-op" nil t)
(majutsu-row-define-copy-transient
 majutsu-op-log-copy-transient
 "Transient for semantic copy commands in `majutsu-op-log-mode'."
 ("o" "Operation id" majutsu-op-log-copy-operation-id))

(defvar-keymap majutsu-op-log-mode-map
  :doc "Keymap for `majutsu-op-log-mode'."
  :parent majutsu-mode-map
  "d" 'majutsu-op-diff-transient
  "u" 'majutsu-op-log-restore-at-point
  "r" 'majutsu-op-log-revert-at-point)

(define-derived-mode majutsu-op-log-mode majutsu-mode "Majutsu Op Log"
  "Major mode for viewing jj operation log."
  :group 'majutsu
  (setq-local line-number-mode nil)
  (setq-local revert-buffer-function #'majutsu-refresh-buffer)
  (setq-local filter-buffer-substring-function
              #'majutsu-row-filter-buffer-substring)
  (add-hook 'kill-buffer-hook #'majutsu-selection-session-end-if-owner nil t))

(put 'majutsu-op-log-mode 'majutsu-op-log-default-arguments
     '("--limit=64"))

(defclass majutsu-op-log-prefix (transient-prefix)
  ((major-mode :initform 'majutsu-op-log-mode)))

(cl-defmethod transient-init-value ((obj majutsu-op-log-prefix))
  (oset obj value
        (majutsu-transient-default-value
         'majutsu-op-log
         (oref obj major-mode)
         'majutsu-op-log-current-arguments
         'majutsu-op-log-default-arguments)))

(cl-defmethod transient-set-value ((obj majutsu-op-log-prefix))
  (let* ((obj (oref obj prototype))
         (mode (or (oref obj major-mode) major-mode))
         (args (transient-args (oref obj command))))
    (put mode 'majutsu-op-log-current-arguments args)
    (transient--history-push obj)
    (when (eq major-mode mode)
      (setq-local majutsu-op-log--args args))))

(cl-defmethod transient-save-value ((obj majutsu-op-log-prefix))
  (let* ((obj (oref obj prototype))
         (mode (or (oref obj major-mode) major-mode))
         (args (transient-args (oref obj command))))
    (put mode 'majutsu-op-log-current-arguments args)
    (setf (alist-get (majutsu-transient-global-default-key
                      'majutsu-op-log mode)
                     transient-values)
          args)
    (transient-save-values)
    (transient--history-push obj)
    (when (eq major-mode mode)
      (setq-local majutsu-op-log--args args))))

(transient-define-argument majutsu-op-log:--limit ()
  :description "Limit"
  :class 'transient-option
  :shortarg "-n"
  :argument "--limit="
  :reader #'transient-read-number-N0)

(transient-define-argument majutsu-op-log:--reversed ()
  :description "Reverse order"
  :class 'transient-switch
  :key "-r"
  :argument "--reversed")

(transient-define-argument majutsu-op-log:--no-graph ()
  :description "Hide graph"
  :class 'transient-switch
  :shortarg "-G"
  :argument "--no-graph")

;;;###autoload(autoload 'majutsu-op-log-transient "majutsu-op" nil t)
(transient-define-prefix majutsu-op-log-transient ()
  "Transient for jj operation log."
  :man-page "jj-operation-log"
  :transient-non-suffix t
  :class 'majutsu-op-log-prefix
  [:description
   "JJ Operation Log"
   ["Options"
    (majutsu-op-log:--limit)
    (majutsu-op-log:--reversed)
    (majutsu-op-log:--no-graph)]
   ["Actions"
    ("l" "Open log" majutsu-op-log)
    ("RET" "Open log" majutsu-op-log)
    ("s" "Save as default" transient-save-and-exit)
    ]])

;;;###autoload
(defun majutsu-op-log (&optional args)
  "Open the Majutsu operation log with ARGS."
  (interactive (list (or (majutsu-op-log-arguments)
                         (get 'majutsu-op-log-mode
                              'majutsu-op-log-default-arguments))))
  (let* ((args (majutsu-op-log--validate-args
                (or args (get 'majutsu-op-log-mode
                              'majutsu-op-log-default-arguments))))
         (root (majutsu--toplevel-safe))
         (repo (file-name-nondirectory (directory-file-name root))))
    (majutsu-setup-buffer #'majutsu-op-log-mode nil
      :buffer (format "*majutsu-op: %s*" repo)
      :directory root
      (majutsu-op-log--args args))))

;;; op restore/revert

(defun majutsu-op--extract-target-operation (args)
  "Return (OPERATION . ARGS) after stripping pseudo --operation= from ARGS."
  (let (operation rest)
    (dolist (arg args)
      (if (string-prefix-p "--operation=" arg)
          (setq operation (substring arg (length "--operation=")))
        (push arg rest)))
    (cons operation (nreverse rest))))

(defun majutsu-op--run-confirmed (action prompt command)
  "Run COMMAND after confirming ACTION with PROMPT."
  (if (not (majutsu-confirm action prompt))
      (message "%s canceled" (capitalize (symbol-name action)))
    (apply #'majutsu-run-jj command)))

(defun majutsu-op-restore (operation)
  "Restore repository state to OPERATION by running jj op restore."
  (interactive (list (majutsu-op--read-operation "Restore to operation")))
  (majutsu-op--run-confirmed
   'op-restore
   (format "Restore repository state to operation %s? " operation)
   (list "op" "restore" operation)))

(defun majutsu-op-revert (operation)
  "Revert OPERATION by running jj op revert."
  (interactive (list (majutsu-op--read-operation "Revert operation")))
  (majutsu-op--run-confirmed
   'op-revert
   (format "Revert operation %s? " operation)
   (list "op" "revert" operation)))

(defun majutsu-op-restore-execute (args)
  "Execute jj op restore with transient ARGS."
  (interactive (list (transient-args 'majutsu-op-restore-transient)))
  (pcase-let* ((`(,operation . ,rest) (majutsu-op--extract-target-operation args)))
    (unless operation
      (user-error "Please select an operation first"))
    (majutsu-op--run-confirmed
     'op-restore
     (format "Restore repository state to operation %s? " operation)
     (append '("op" "restore") rest (list operation)))))

(defun majutsu-op-revert-execute (args)
  "Execute jj op revert with transient ARGS."
  (interactive (list (transient-args 'majutsu-op-revert-transient)))
  (pcase-let* ((`(,operation . ,rest) (majutsu-op--extract-target-operation args)))
    (unless operation
      (user-error "Please select an operation first"))
    (majutsu-op--run-confirmed
     'op-revert
     (format "Revert operation %s? " operation)
     (append '("op" "revert") rest (list operation)))))

;;;###autoload(autoload 'majutsu-op-restore-transient "majutsu-op" nil t)
(transient-define-prefix majutsu-op-restore-transient ()
  "Transient for jj operation restore."
  :man-page "jj-operation-restore"
  :class 'majutsu-op-target-prefix
  :transient-non-suffix t
  :description "JJ Operation Restore"
  [["Selection"
    (majutsu-op-arg:--operation)
    ("c" "Clear selections" majutsu-selection-clear :transient t)]
   ["What"
    ("-r" "Repo state" "--what=repo")
    ("-t" "Remote-tracking" "--what=remote-tracking")]
   ["Actions"
    ("R" "Restore" majutsu-op-restore-execute)
    ("RET" "Restore" majutsu-op-restore-execute)]]
  (interactive)
  (transient-setup 'majutsu-op-restore-transient nil nil
                   :scope (majutsu-selection-session-begin)))

;;;###autoload(autoload 'majutsu-op-revert-transient "majutsu-op" nil t)
(transient-define-prefix majutsu-op-revert-transient ()
  "Transient for jj operation revert."
  :man-page "jj-operation-revert"
  :class 'majutsu-op-target-prefix
  :transient-non-suffix t
  :description "JJ Operation Revert"
  [["Selection"
    (majutsu-op-arg:--operation)
    ("c" "Clear selections" majutsu-selection-clear :transient t)]
   ["What"
    ("-r" "Repo state" "--what=repo")
    ("-t" "Remote-tracking" "--what=remote-tracking")]
   ["Actions"
    ("V" "Revert" majutsu-op-revert-execute)
    ("RET" "Revert" majutsu-op-revert-execute)]]
  (interactive)
  (transient-setup 'majutsu-op-revert-transient nil nil
                   :scope (majutsu-selection-session-begin)))

;;; operation metadata and diff

(defun majutsu-op--metadata (operation)
  "Return line-safe metadata for OPERATION using an internal query."
  (let* ((args (append majutsu-op--read-only-global-args
                       (list "op" "show" operation "--no-op-diff"
                             "-T" majutsu-op--metadata-template)))
         (lines (split-string
                 (apply #'majutsu-jj-buffer-string args) "\n" t)))
    (or (seq-some #'majutsu-op--parse-metadata-line lines)
        (user-error "Failed to parse operation metadata for %s" operation))))

(defun majutsu-op--diff-command-args (args)
  "Return jj arguments for operation diff ARGS."
  (append majutsu-op--read-only-global-args
          (list "--config" (majutsu-op--commit-summary-config-arg)
                "op" "diff" "--no-graph")
          args))

(defun majutsu-op--diff-line-marker (marker)
  "Return display text for diff line MARKER."
  (propertize marker 'face (if (string= marker "+") 'diff-added 'diff-removed)))

(defun majutsu-op--diff-line-flags (value)
  "Return display flag text for parsed diff line VALUE."
  (let ((flags (delq nil (list (and (plist-get value :hidden) "hidden")
                               (and (plist-get value :conflict) "conflict")
                               (and (plist-get value :empty) "empty")))))
    (unless (null flags)
      (concat " "
              (mapconcat (lambda (flag) (format "(%s)" flag)) flags " ")))))

(defun majutsu-op--format-diff-line (value)
  "Return display text for parsed operation diff line VALUE."
  (let ((prefix (plist-get value :prefix)))
    (concat (majutsu-op--diff-line-marker (plist-get value :marker))
            " "
            (when prefix
              (concat (propertize prefix 'face 'font-lock-keyword-face) " "))
            (if (plist-get value :absent)
                (propertize "(absent)" 'face 'shadow)
              (concat (plist-get value :change-id-short-display)
                      " "
                      (plist-get value :commit-id-short-display)
                      (or (majutsu-op--diff-line-flags value) "")
                      " "
                      (plist-get value :description))))))

(defun majutsu-op--node-display-text (node)
  "Return NODE text for display, preserving text properties."
  (plist-get node :text))

(defun majutsu-op--insert-diff-node (node)
  "Insert one parsed operation diff NODE."
  (pcase (plist-get node :type)
    ('group
     (magit-insert-section (jj-op-group (list :kind (plist-get node :kind)
                                              :title (plist-get node :title)))
       (magit-insert-heading (majutsu-op--node-display-text node))
       (dolist (child (plist-get node :children))
         (majutsu-op--insert-diff-node child))))
    ('ref
     (magit-insert-section (jj-op-ref (plist-get node :name))
       (magit-insert-heading (majutsu-op--node-display-text node))
       (dolist (child (plist-get node :children))
         (majutsu-op--insert-diff-node child))))
    ('commit-line
     (let ((value (plist-get node :value)))
       (magit-insert-section (jj-op-commit-line value)
         (if-let* ((commit-id (plist-get value :commit-id)))
             (magit-insert-section (jj-commit commit-id)
               (magit-insert-heading (majutsu-op--format-diff-line value)))
           (magit-insert-heading (majutsu-op--format-diff-line value))))))
    ('ref-line
     (let ((value (plist-get node :value)))
       (magit-insert-section (jj-op-ref-line value)
         (if-let* ((commit-id (and (not (plist-get value :absent))
                                   (plist-get value :commit-id))))
             (magit-insert-section (jj-commit commit-id)
               (magit-insert-heading (majutsu-op--format-diff-line value)))
           (magit-insert-heading (majutsu-op--format-diff-line value))))))
    ('warning
     (magit-insert-section (jj-op-warning nil)
       (magit-insert-heading (propertize (majutsu-op--node-display-text node)
                                         'face 'font-lock-warning-face))))
    ('elision
     (magit-insert-section (jj-op-elision nil)
       (magit-insert-heading (propertize (string-trim-left
                                          (majutsu-op--node-display-text node))
                                         'face 'shadow))))
    (_
     (magit-insert-section (jj-op-raw-line nil)
       (magit-insert-heading (majutsu-op--node-display-text node))))))

(defun majutsu-op--diff-line-string ()
  "Return the current diff line, preserving text properties."
  (buffer-substring (line-beginning-position) (line-end-position)))

(defun majutsu-op--delete-line ()
  "Delete current line, including trailing newline if present."
  (delete-region (line-beginning-position)
                 (min (point-max) (1+ (line-end-position)))))

(defun majutsu-op--diff-group-boundary-p ()
  "Return non-nil when point starts a new op-diff group or separator."
  (or (eobp)
      (let* ((line (majutsu-op--diff-line-string))
             (plain (substring-no-properties line))
             (trimmed (string-trim plain)))
        (or (string-empty-p trimmed)
            (majutsu-op--diff-operation-header-p plain)
            (majutsu-op--diff-group-kind plain)))))

(defun majutsu-op--diff-ref-boundary-p (group-kind)
  "Return non-nil when point leaves the current ref block in GROUP-KIND."
  (or (majutsu-op--diff-group-boundary-p)
      (let* ((line (majutsu-op--diff-line-string))
             (plain (substring-no-properties line)))
        (majutsu-op--diff-ref-header-p plain group-kind))))

(defun majutsu-op--wash-diff-line (group-kind &optional ref-name)
  "Wash one operation diff line in GROUP-KIND under REF-NAME."
  (let* ((colored-line (majutsu-op--diff-line-string))
         (plain-line (substring-no-properties colored-line))
         (trimmed (string-trim plain-line)))
    (cond
     ((string-empty-p trimmed)
      (majutsu-op--delete-line))
     ((majutsu-op--diff-warning-line-p plain-line)
      (majutsu-op--delete-line)
      (majutsu-op--insert-diff-node (list :type 'warning :text colored-line)))
     ((majutsu-op--diff-elision-line-p plain-line)
      (majutsu-op--delete-line)
      (majutsu-op--insert-diff-node (list :type 'elision :text colored-line)))
     ((majutsu-op--parse-diff-marker-line
       plain-line colored-line group-kind ref-name)
      (let ((node (majutsu-op--parse-diff-marker-line
                   plain-line colored-line group-kind ref-name)))
        (majutsu-op--delete-line)
        (majutsu-op--insert-diff-node node)))
     (t
      (majutsu-op--delete-line)
      (majutsu-op--insert-diff-node (list :type 'raw-line :text colored-line))))))

(defun majutsu-op--wash-diff-ref (group-kind)
  "Wash one named ref block in ref GROUP-KIND."
  (let* ((line (majutsu-op--diff-line-string))
         (plain (substring-no-properties line))
         (name (string-remove-suffix ":" (string-trim plain))))
    (majutsu-op--delete-line)
    (magit-insert-section (jj-op-ref name)
      (magit-insert-heading line)
      (while (and (not (eobp))
                  (not (majutsu-op--diff-ref-boundary-p group-kind)))
        (majutsu-op--wash-diff-line group-kind name)))))

(defun majutsu-op--wash-diff-group ()
  "Wash one top-level operation diff group at point."
  (let* ((line (majutsu-op--diff-line-string))
         (plain (substring-no-properties line))
         (title (string-trim plain))
         (kind (majutsu-op--diff-group-kind plain)))
    (majutsu-op--delete-line)
    (magit-insert-section (jj-op-group (list :kind kind :title title))
      (magit-insert-heading line)
      (while (and (not (eobp))
                  (not (majutsu-op--diff-group-boundary-p)))
        (if (and (majutsu-op--diff-ref-group-p kind)
                 (let* ((next-line (majutsu-op--diff-line-string))
                        (next-plain (substring-no-properties next-line)))
                   (majutsu-op--diff-ref-header-p next-plain kind)))
            (majutsu-op--wash-diff-ref kind)
          (majutsu-op--wash-diff-line kind))))))

(defun majutsu-op--wash-diff-output (_args)
  "Wash raw `jj op diff` output in the current narrowed region."
  (goto-char (point-min))
  (while (not (eobp))
    (let* ((line (majutsu-op--diff-line-string))
           (plain (substring-no-properties line))
           (trimmed (string-trim plain)))
      (cond
       ((string-empty-p trimmed)
        (majutsu-op--delete-line))
       ((majutsu-op--diff-operation-header-p plain)
        (majutsu-op--delete-line))
       ((majutsu-op--diff-group-kind plain)
        (majutsu-op--wash-diff-group))
       (t
        (majutsu-op--wash-diff-line nil)))))
  (when (= (point-min) (point-max))
    (insert (propertize "No repository changes\n" 'face 'shadow))))

(defun majutsu-op--insert-operation-diff (args)
  "Insert washed operation diff for ARGS."
  (apply #'majutsu-jj-wash
         #'majutsu-op--wash-diff-output
         nil
         (majutsu-op--diff-command-args args)))

(defun majutsu-op-diff-default-action ()
  "Open evolog for a changed line, or visit the ordinary thing at point."
  (interactive)
  (if (majutsu-op--diff-line-at-point)
      (majutsu-op-diff-evolog-at-point)
    (majutsu-visit-thing)))

(defun majutsu-op-diff-evolog-at-point ()
  "Open evolog for the operation diff line at point."
  (interactive)
  (if-let* ((line (majutsu-op--diff-line-at-point))
            (revset (majutsu-op--diff-line-evolog-revset line)))
      (if (fboundp 'majutsu-evolog)
          (funcall #'majutsu-evolog revset)
        (user-error "Evolog support is not implemented yet"))
    (user-error "No changed commit at point")))

;;; op diff

(defvar-local majutsu-op-diff--args nil
  "Arguments used for the current operation diff buffer.")

(defun majutsu-op--diff-arg-value (prefix args)
  "Return the value for PREFIX in ARGS."
  (seq-some (lambda (arg)
              (and (string-prefix-p prefix arg)
                   (substring arg (length prefix))))
            args))

(defun majutsu-op--diff-buffer-heading (args)
  "Return a heading for operation diff ARGS."
  (cond
   ((majutsu-op--diff-arg-value "--operation=" args)
    (format "Operation Diff %s"
            (majutsu-op--diff-arg-value "--operation=" args)))
   ((or (majutsu-op--diff-arg-value "--from=" args)
        (majutsu-op--diff-arg-value "--to=" args))
    (format "Operation Diff %s..%s"
            (or (majutsu-op--diff-arg-value "--from=" args) "@-")
            (or (majutsu-op--diff-arg-value "--to=" args) "@")))
   (t "Operation Diff")))

(defun majutsu-op--insert-metadata-field (label value)
  "Insert operation metadata LABEL and VALUE."
  (insert (propertize label 'face 'font-lock-keyword-face)
          ": " (or value "") "\n"))

(defun majutsu-op--insert-diff-endpoint (label operation)
  "Insert metadata for operation diff endpoint LABEL and OPERATION."
  (let* ((metadata (majutsu-op--metadata operation))
         (op-id (plist-get metadata :op-id)))
    (magit-insert-section
        (jj-op-diff-endpoint (list :label label :op-id op-id))
      (magit-insert-heading
        (format "%s operation %s" label
                (or (plist-get metadata :op-id-short) op-id)))
      (majutsu-op--insert-metadata-field "Id" op-id)
      (majutsu-op--insert-metadata-field "User"
                                         (plist-get metadata :user))
      (majutsu-op--insert-metadata-field "Workspace"
                                         (plist-get metadata :workspace))
      (majutsu-op--insert-metadata-field "Ended"
                                         (plist-get metadata :end-time))
      (majutsu-op--insert-metadata-field "Description"
                                         (plist-get metadata :desc)))))

(defun majutsu-op--insert-diff-endpoints (args)
  "Insert metadata for explicit from/to operation diff ARGS."
  (let ((from (majutsu-op--diff-arg-value "--from=" args))
        (to (majutsu-op--diff-arg-value "--to=" args)))
    (when (or from to)
      (magit-insert-section (jj-op-diff-endpoints nil)
        (magit-insert-heading "Operations")
        (when from
          (majutsu-op--insert-diff-endpoint "From" from))
        (when to
          (majutsu-op--insert-diff-endpoint "To" to))))))

(defun majutsu-op-diff-arguments ()
  "Return operation diff arguments from the active transient, if any."
  (if (eq transient-current-command 'majutsu-op-diff-transient)
      (transient-args 'majutsu-op-diff-transient)
    (list (concat "--operation="
                  (majutsu-op--read-operation "Diff operation")))))

(defun majutsu-op-diff-refresh-buffer ()
  "Refresh the current operation diff buffer."
  (interactive)
  (majutsu--assert-mode 'majutsu-op-diff-mode)
  (let ((args (or majutsu-op-diff--args '())))
    (magit-insert-section (jj-op-diff-buffer args)
      (magit-insert-heading (majutsu-op--diff-buffer-heading args))
      (majutsu-op--insert-diff-endpoints args)
      (majutsu-op--insert-operation-diff args)))
  (majutsu-selection-render))

(defvar-keymap majutsu-op-diff-mode-map
  :doc "Keymap for `majutsu-op-diff-mode'."
  :parent majutsu-mode-map
  "<remap> <majutsu-visit-thing>" 'majutsu-op-diff-default-action
  "v" 'majutsu-op-diff-evolog-at-point)

(define-derived-mode majutsu-op-diff-mode majutsu-mode "Majutsu Op Diff"
  "Major mode for comparing jj operations."
  :group 'majutsu
  (setq-local line-number-mode nil)
  (setq-local revert-buffer-function #'majutsu-refresh-buffer)
  (add-hook 'kill-buffer-hook #'majutsu-selection-session-end-if-owner nil t))

(defun majutsu-op-diff--buffer-name ()
  "Return buffer name for operation diff."
  (let* ((root (majutsu--toplevel-safe))
         (repo (file-name-nondirectory (directory-file-name root))))
    (format "*majutsu-op-diff: %s*" repo)))

;;;###autoload
(defun majutsu-op-diff (&optional args)
  "Open an operation diff buffer with ARGS."
  (interactive (list (majutsu-op-diff-arguments)))
  (let ((root (majutsu--toplevel-safe)))
    (majutsu-setup-buffer #'majutsu-op-diff-mode nil
      :buffer (majutsu-op-diff--buffer-name)
      :directory root
      (majutsu-op-diff--args args))))

;;;###autoload(autoload 'majutsu-op-diff-transient "majutsu-op" nil t)
(transient-define-prefix majutsu-op-diff-transient ()
  "Transient for jj operation diff."
  :man-page "jj-operation-diff"
  :class 'majutsu-op-target-prefix
  :transient-non-suffix t
  :incompatible '(("--operation=" "--from=")
                  ("--operation=" "--to="))
  [:description
   "JJ Operation Diff"
   ["Selection"
    (majutsu-op-arg:--operation)
    (majutsu-op-arg:--from)
    (majutsu-op-arg:--to)
    ("c" "Clear selections" majutsu-selection-clear :transient t)]
   ["Actions"
    ("d" "Open diff" majutsu-op-diff)
    ("RET" "Open diff" majutsu-op-diff)
    ]]
  (interactive)
  (transient-setup 'majutsu-op-diff-transient nil nil
                   :scope (majutsu-selection-session-begin)))

;;; _
(provide 'majutsu-op)
;;; majutsu-op.el ends here
